100 REM    MONOPOLE.BAS        NOVEMBER 30, 1991           J. KANTELE

110 COLOR 14, 6: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE X(E0/E2), B(E2), AND RHO^2 VALUES IN CASES "

150 PRINT " INVOLVING COMPETING E0 AND E2 TRANSITIONS FROM A O+ STATE. THE "

160 PRINT " BOHR-MOTTELSON s.p. VALUES FOR RHO^2 AND X ARE ALSO COMPUTED. "

170 PRINT

180 PRINT "*****************************************************************"

190 PRINT

200 PRINT " ENTER THE FOLLOWING QUANTITIES:"

210 PRINT

220 INPUT " MASS NUMBER OF THE NUCLEUS, A = ", A

230 INPUT " ENERGY OF THE E2 GAMMA-RAY (IN MeV) = ", G

240 INPUT " ELECTRONIC FACTOR OMEGA(K) = ", O

250 INPUT " RELATIVE INTENSITY OF THE K-LINE OF THE E0 = ", IK0

260 PRINT " RELATIVE ITENSITY OF THE  E2 K-LINE  (GIVE GAMMA INTENSITY,"

270 INPUT " IF KNOWN, AND PUT A(K) = 1 IN NEXT ENTRY), I(K,E2) = ", IK2

280 INPUT " INTERNAL CONVERSION COEFFICIENT OF THE E2, ALPHA(K,E2) = ", AK

290 INPUT " PARTIAL HALF-LIFE OF THE E2 GAMMA (SEC) = ", T

300 PRINT

310 LET X = (2.56E+09) * A ^ (4 / 3) * G ^ 5 * IK0 * AK / IK2 / O

320 LET B2 = 56.4 / T / G ^ 5 * (1E-15)

330 LET R = X * B2 / (.0002074) / A ^ (4 / 3)

340 PRINT USING " X(E0/E2) = ##.##^^^^"; X

350 PRINT

360 PRINT USING " B(E2) = ##.##^^^^ e^2b^2"; B2

370 PRINT

380 PRINT USING " RHO^2 = ##.##^^^^"; R

390 PRINT

400 RSP = .5 * A ^ (-2 / 3): XSP = 17 * A ^ (-2 / 3)

410 PRINT USING " BOHR-MOTTELSON s.p. UNIT: RHO^2(s.p.) = ##.##^^^^ "; RSP

420 PRINT

430 PRINT USING " BOHR-MOTTELSON s.p. UNIT: X(s.p.) = ##.##^^^^ "; XSP

440 PRINT

450 COLOR 11: PRINT " TO QUIT, JUST HIT CTRL-C ": COLOR 14

460 PRINT "*****************************************************************"

470 GOTO 190

