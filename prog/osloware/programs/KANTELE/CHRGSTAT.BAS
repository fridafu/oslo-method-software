100 REM    CHRGSTAT.BAS        MAY 11, 1993           J. KANTELE

110 COLOR 15, 1: CLS

120 PRINT "****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE THE AVERAGE EQUILIBRIUM CHARGE-STATE OF "

150 PRINT " HEAVY IONS IN MATTER (e.g., BEHIND VARIOUS FOILS). THE PROGRAM "

160 PRINT " IS BASED ON EMPIRICAL FORMULA BY SHIMA et al., NIM 200 (1982) "

170 PRINT " 605 [Sh82], AND IS VALID FOR Z1 > 7; 3 < Z2 < 80; E < 6 MeV/u. "

180 PRINT

190 PRINT "****************************************************************"

200 PRINT

210 INPUT " ENERGY OF ION (MeV), E  = ", E

220 PRINT

230 INPUT " MASS OF ION (a.m.u.), M  = ", M

240 PRINT

250 INPUT " ATOMIC NUMBER OF ION, Z1  = ", Z1

260 PRINT

270 INPUT " ATOMIC NUMBER OF STOPPING MEDIUM, Z2 = ", Z2

280 PRINT

290 V = SQR(E ^ 2 + 2 * E * M * 931.5) / (E + M * 931.5) * 2.998E+10

300 X = V / (3.6E+08 * Z1 ^ .45)

310 Y = 1 - .00119 * (Z2 - 6) * SQR(X) + .00001 * (Z2 - 6) ^ 2 * X

320 Q = Z1 * (1 - EXP(-1.25 * X + .32 * X ^ 2 - .11 * X ^ 3)) * Y

330 COLOR 14

340 PRINT USING " THE AVERAGE EQUILIBRIUM CHARGE IS Q(ave) = ##.## "; Q

350 PRINT : COLOR 15

360 PRINT " TO QUIT, HIT CTRL-C "

370 PRINT : GOTO 190

380 END

