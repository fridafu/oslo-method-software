100 REM    ELECSTOP.BAS        OCTOBER 9, 1990         J. KANTELE

110 COLOR 15, 1: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE EXTRAPOLATED AND 99 % STOPPING RANGES, AND"

150 PRINT " STOPPING POWERS FOR MONOKINETIC ELECTRONS FROM 1 keV TO 30 MeV."

160 PRINT " IN dE/dx CALCULATIONS, DATA FOR Z = 13 AND 82 FROM ICRU-37 ARE"

170 PRINT " USED. BETWEEN 10 keV AND 30 MeV, THE ACCURACY OF THE CALCULATED"

180 PRINT " dE/dx IS USUALLY 10 PER CENT; FOR SMALL INTERPOLATION STEPS, "

190 PRINT " THE ACCURACY OF THE TOTAL STOPPING POWER IS 5 PER CENT."

200 PRINT " THE RANGE FORMULAE ARE BASED ON TABATA et al., NIM 103 (1972) 85,"

210 PRINT " ON A FITTED FORMULA FOR A = A(Z) (3 PER CENT ACCURACY), AND ON A"

220 PRINT " RE-ANALYSIS OF MANY EXPERIMENTAL DATA IN THE LITERATURE. "

230 PRINT

240 PRINT " FOR 1 keV < E < 30 MeV AND 3 < Z < 100, THE UNCERTAINTY OF R(ex)"

250 PRINT " IS 5 TO 10 PER CENT. THE R(99) VALUES ARE MOSTLY GOOD TO < 15 %"

260 PRINT

270 PRINT "*****************************************************************"

280 DIM T(3, 12)

290 FOR K = 1 TO 3

300     FOR L = 1 TO 12

310        READ T(K, L)

320     NEXT L

330 NEXT K

340 DATA 10,20,50,100,200,500,1000,2000,5000,10000,20000,30000

350 DATA 16.5,9.85,5.046,3.185,2.183,1.604,1.486,1.518,1.69,1.921,2.34,2.746

360 DATA 8.448,5.480,3.034,2.008,1.442,1.135,1.123,1.256,1.70,2.407,3.83,4.55

370 PRINT

380 PRINT " WANT TO SEE A DENSITY TABLE, TO GET RANGE IN mm? (1 = YES, 2 = NO)"

390 INPUT " IF DENSITY IS UNKNOWN, THE RANGE COMES IN mg/cm^2. CHOICE = ", N

400 IF N = 1 THEN GOSUB 1000 ELSE 410

410 PRINT

420 INPUT " ABSORBER DENSITY (g/cm^3); (ENTER A NUMBER > 0 IF UNKNOWN) = ", RHO

430 PRINT

440 INPUT " DID YOU JUST GIVE THE CORRECT DENSITY? (1 = YES, 2 = NO) ", N

450 PRINT

460 INPUT " ABSORBER Z = ", Z

470 PRINT

480 PRINT " THE dE/dx CAN BE CALCULATED BETWEEN 10 keV TO 30 MeV, THE RANGE VALUES "

490 PRINT

500 INPUT " FROM 1 keV TO 30 MeV. ENTER ELECTRON ENERGY (in keV) = ", E

510 PRINT

520 FOR I = 1 TO 6

530 READ Z(I), C(I)

540 DATA 9, 1.047, 11, 1.036, 18, 1.065, 20, .952, 23, 1.035, 34, 1.033

550 IF Z = Z(I) THEN C = C(I): GOTO 580

560 NEXT I

570 C = 1

580 A = C * EXP(1.4594 + .21579 * LOG(Z) + .24867 * LOG(Z) ^ 2 - .02225 * LOG(Z) ^ 3)

590 C1 = 233.5 * A / Z ^ 1.209: C2 = .000178 * Z: C3 = .9891 - .000301 * Z

600 C4 = 1.468 - .0118 * Z: C5 = 1.232 / Z ^ .109: T = E / 511

610 IF Z = 4 THEN C0 = .87 ELSE C0 = 1

620 R = C0 * C1 * ((1 / C2) * LOG(1 + C2 * T) - C3 * T / (1 + C4 * T ^ C5))

630 R99 = (1.13 + .0058 * Z) * R

640 IF N = 1 THEN REX = R / (RHO * 100)

650 IF N = 1 THEN RM99 = R99 / (RHO * 100)

660 IF N = 1 THEN 710

670 COLOR 11: PRINT USING " EXTRAPOLATED RANGE R(ex) = ##.##^^^^ mg/cm^2"; R

680 PRINT : COLOR 15

690 COLOR 14: PRINT USING " 99-PER-CENT-STOPPING RANGE R(99) = ##.##^^^^ mg/cm^2"; R99

700 COLOR 15: IF N <> 1 THEN 750

710 REM

720 COLOR 11: PRINT USING " EXTRAPOLATED RANGE R(ex) = ##.##^^^^ mg/cm^2 = ##.##^^^^ mm "; R; REX

730 PRINT

740 COLOR 14: PRINT USING " 99-PER-CENT-STOPPING RANGE R(99) = ##.##^^^^ mg/cm^2 = ##.##^^^^ mm "; R99; RM99

750 COLOR 15: FOR J = 1 TO 10

760 IF T(1, J + 1) - E >= 0 GOTO 780

770 NEXT J

780 FOR I = 2 TO 3

790 GOSUB 960

800 NEXT I

810 LET S = D(2) + (D(3) - D(2)) * (Z - 13) / (82 - 13)

820 PRINT : IF E < 10 OR E > 30000 THEN 850

830 PRINT USING " TOTAL STOPPING POWER dE/dx = ##.## keV/mg/cm^2"; S

840 PRINT

850 PRINT "*****************************************************************"

860 PRINT

870 PRINT " DO YOU WISH TO CONTINUE ?  ENTER YOUR CHOICE BY NUMBER: "

880 PRINT

890 INPUT " 1 = SAME Z; 2 = CHANGE Z; 3 = CHANGE Z AND DENSITY; 4 = QUIT. CHOICE = ", M

900 PRINT

910 RESTORE

920 PRINT "*****************************************************************"

930 ON M GOTO 470, 940, 370, 950

940 N = 2: GOTO 450

950 END

960 X = LOG(T(1, J + 1)) - LOG(T(1, J))

970 Y = LOG(T(I, J + 1)) - LOG(T(I, J))

980 D(I) = T(I, J) * EXP((LOG(E) - LOG(T(1, J))) / X * Y)

990 RETURN

1000 PRINT

1010 PRINT "*****************************************************************"

1020 COLOR 14, 4

1030 PRINT " DENSITIES (g/cm^3) AND Z OR Z(eff) VALUES OF SOME COMMONLY USED MATERIALS:     "

1040 PRINT " Be: 1.85/4; Al: 2.7/13; Si: 2.33/14; Fe: 7.87/26; Cu: 8.96/29; Ge: 5.32/32;    "

1050 PRINT " Cd: 8.65/48; Au: 19.3/79; Pb: 11.35/82; U: 18.95/92; NE Plastic Scint.:        "

1060 PRINT " 1.032/6.5; NaI: 3.67/46.6; BaF2: 4.89/43.8; BGO: 7.13/62.5;                    "

1070 PRINT " Lucite: 1.19/6.6; Paraffin: .85/5; Polyethylene: .94/5.5;                      "

1080 PRINT " Mylar: 1.4/7; Glass: 2.5/8  (SOME LOW Z(eff) VALUES ARE ONLY ESTIMATES)        "

1090 PRINT

1100 PRINT " FOR A COMPOSITE ABSORBER, Z = Z(eff) = [Sum of a(i) * Z(i)],                   "

1110 PRINT " WHERE a(i) = FRACTION BY WEIGHT OF ELEMENT Z(i). Sum of the a(i)'s = 1.        "

1120 COLOR 15, 1

1130 PRINT "*****************************************************************               "

1140 RETURN



