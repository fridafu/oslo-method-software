100 REM   HALFLIFE.BAS        APRIL 28, 1993     LHERSONNEAU/KANTELE

110 REM

120 COLOR 14, 5: CLS : PRINT

130 PRINT "******************************************************************"

140 PRINT

150 PRINT " PROGRAM TO CALCULATE THE HALF-LIFE OF A SINGLE-COMPONENT DECAY;"

160 PRINT " A CONSTANT BACKGROUND WILL BE SUBTRACTED FROM EACH CHANNEL. "

170 PRINT " NOTE: THE PROGRAM ONLY GIVES THE STATISTICAL ERROR, NO OTHER "

180 PRINT " ONES, SUCH AS THOSE DUE TO TIME CALIBRATION, NON-LINEARITY OF THE "

190 PRINT " TAC, PILE-UP, POSSIBLE RANDOM COINCIDENCES, ETC."

200 PRINT

210 PRINT " THE NUMBER OF COUNTS MAY DECREASE OR INCREASE WITH INCREASING "

220 PRINT " CHANNEL NUMBER (DEPENDING ON THE EXPERIMENTAL ARRANGEMENT). "

230 PRINT

240 PRINT "******************************************************************"

250 PRINT

260 PRINT " DOES YOUR COUNTING RATE DECREASE (1) OR INCREASE (2) WITH THE "

270 INPUT " CHANNEL NUMBER? ANSWER (1 OR 2) = ", C

280 PRINT

290 PRINT " GIVE THE TIME CALIBRATION. THE CHANNEL WIDTH UNITS FROM WHICH TO "

300 PRINT " CHOOSE ARE: (1) ps, (2) ns, (3) microsec, (4) ms, (5) s, (6) min,"

310 INPUT " (7) h, (8) d, (9) a. MAKE YOUR CHOICE BY NUMBER (1...8), NO = ", NO

320 PRINT

330 ON NO GOSUB 840, 850, 860, 870, 880, 890, 900, 910, 920

340 INPUT " ENTER VALUE OF THE TIME-CHANNEL WIDTH IN THE CHOSEN UNIT, W = ", W1

350 PRINT

360 INPUT " VALUE OF THE CONSTANT BACKGROUND (PER CHANNEL), B0 = ", B0

370 PRINT

380 INPUT " NUMBER OF POINTS TO BE ENTERED (AT LEAST 3 PTS), N = ", N

390 PRINT

400 N0 = 0

410 J = 0

420 K = 0

430 L = 0

440 M = 0

450 R2 = 0

460 FOR I = 1 TO N

470 PRINT " ENTER CHANNEL-NUMBER AND NUMBER OF COUNTS X, Y OF POINT"; I;

480 PRINT

490 INPUT " = ", X1, Y

500 X = W1 * X1

510 Y = Y - B0

520 W = (Y * Y) / (Y + B0)

530 Z = LOG(Y)

540 N0 = N0 + W

550 J = J + W * X

560 K = K + W * Z

570 L = L + W * X ^ 2

580 M = M + W * Z ^ 2

590 R2 = R2 + W * X * Z

600 NEXT I

610 D0 = N0 * L - J ^ 2

620 B = (N0 * R2 - K * J) / D0

630 A = (L * K - J * R2) / D0

640 PRINT : COLOR 15

650 IF C = 1 THEN SI = -1 ELSE SI = 1

660 T = SI * LOG(2) / B

670 PRINT USING " HALF-LIFE = ##.###^^^^ \      \ "; T; A$

680 PRINT

690 ER1 = SQR(N0 / D0)

700 LET ER2 = ABS(ER1 * T / (-B))

710 PRINT USING " STATISTICAL ERROR = ##.##^^^^ \      \ "; ER2; A$

720 PRINT : COLOR 14

730 PRINT "*****************************************************************"

740 PRINT

750 PRINT " THE ALTERNATIVES TO CONTINUE ARE:"

760 PRINT

770 PRINT " 1. CONTINUE WITH THE SAME TIME CALIBRATION "

780 PRINT " 2. GIVE ANOTHER TIME CALIBRATION "

790 PRINT " 3. QUIT "

800 PRINT

810 INPUT " YOUR CHOICE (1...3) = ", NC

820 ON NC GOTO 370, 200, 830

830 END

840 A$ = "ps": RETURN

850 A$ = "ns": RETURN

860 A$ = "microsec": RETURN

870 A$ = "ms": RETURN

880 A$ = "s": RETURN

890 A$ = "min": RETURN

900 A$ = "h": RETURN

910 A$ = "d": RETURN

920 A$ = "a": RETURN

