100 REM    ELECTRON.BAS        OCTOBER 8, 1990          J. KANTELE

110 COLOR 14, 5: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE ELECTRON BRHO, SPEED, AND EXTRAPOLATED AND"

150 PRINT " 99-PER-CENT STOPPING RANGES IN ELEMENTS. THE ACCURACY OF R(ex) "

160 PRINT " FOR Al AND Pb IS 5 PER CENT; FOR OTHER ELEMENTS, ABOUT 10 TO"

170 PRINT " 15 PER CENT. R(ex) STOPS COMPLETELY BETA-RAY SPECTRA WITH"

180 PRINT " E(max) = E. THE R(99) RESULTS ARE GOOD TO < 20 % ."

190 PRINT " THE RANGE CALCULATION IS BASED ON A COMBINATION OF THE RESULTS "

200 PRINT " OF TABATA et al., NIM 103 (1972) 85, AND ON THE PRESENT WORK. "

210 PRINT

220 PRINT " THE RANGE ACCURACY MAY BE WORSE OUTSIDE 5 < Z < 100  AND "

230 PRINT " 50 keV < E < 30 MeV. FOR Al, AND Pb, THE LOWER LIMIT IS 5 keV. "

240 PRINT

250 PRINT "*****************************************************************"

260 PRINT

270 INPUT " ABSORBER Z = ", Z

280 PRINT

290 INPUT " ELECTRON ENERGY (in keV) = ", E

300 REM

310 BRHO = SQR(E ^ 2 + 1022 * E) / 299.79

320 V1 = SQR(E ^ 2 + 1022 * E) / (E + 511)

330 V2 = 29.979 * V1

340 PRINT

350 IF Z < 7 THEN C = 1.15 ELSE C = 1

360 D = EXP(-2.824 + .0403225 * LOG(E) + .3511 * LOG(E) ^ 2 - .033565 * LOG(E) ^ 3 + .0009703 * LOG(E) ^ 4)

370 R = C * (D - (82 - Z) / (82 - 13) * (D - (EXP(-3.956 + .465 * LOG(E) + .4 * LOG(E) ^ 2 - .0512 * LOG(E) ^ 3 + .00198 * LOG(E) ^ 4))))

380 R99 = R * (1.13 + .0058 * Z)

390 COLOR 15: PRINT USING " BRHO = ###.## Tmm"; BRHO

400 PRINT

410 PRINT USING " VELOCITY = ##.### cm/ns; v/c = #.####"; V2; V1

420 PRINT

430 PRINT USING " R(ex) IN Z = ## IS = ##.##^^^^ mg/cm^2; R(99) = ##.##^^^^ mg/cm^2 "; Z; R; R99

440 PRINT

450 COLOR 14: PRINT "*****************************************************************"

460 PRINT

470 PRINT " DO YOU WISH TO CONTINUE? ENTER YOUR CHOICE BY NUMBER: "

480 PRINT

490 INPUT " 1 = THE SAME Z;  2 = ANOTHER Z;  3 = QUIT.  YOUR CHOICE = ", M

500 PRINT

510 PRINT "*****************************************************************"

520 ON M GOTO 280, 260, 530

530 END



