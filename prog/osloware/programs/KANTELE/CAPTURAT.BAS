100 REM    CAPTURAT.BAS          MARCH 15, 1990          J. KANTELE

110 COLOR 15, 2: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE CAPTURE RATIOS IN ELECTRON-CAPTURE DECAY."

150 PRINT " THE PROCEDURE IS ADOPTED FROM: TABLE OF ISOTOPES (1978), p. 576."

160 PRINT

170 DIM T(9, 12)

180 FOR K = 1 TO 9

190     FOR L = 1 TO 12

200         READ T(K, L)

210     NEXT L

220 NEXT K

230 DATA 15,20,25,30,35,40,50,60,70,80,90,100

240 DATA 2.15,4.04,6.54,9.66,13.47,18,29.2,43.6,61.3,83.1,109.65,143.1

250 DATA .19,.44,.77,1.194,1.78,2.53,4.47,7.13,10.49,14.84,20.47,27.7

260 DATA .136,.35,.652,1.04,1.6,2.31,4.16,6.72,9.98,14.21,19.67,26.8

270 DATA .135,.347,.641,1.02,1.55,2.22,3.93,6.21,8.94,12.28,16.3,20.9

280 DATA .079,.084,.088,.093,.098,.104,.114,.125,.137,.150,.167,.181

290 DATA .0017,.0028,.0043,.006,.0078,.01,.0142,.0186,.0225,.0252,.027,.029

300 DATA .0017,.003,.0047,.007,.0097,.013,.022,.0345,.0525,.077,.11,.155

310 DATA .025,.05,.086,.113,.137,.158,.193,.222,.247,.267,.282,.30

320 PRINT "*****************************************************************"

330 PRINT

340 INPUT " WOULD YOU LIKE TO PRINT YOUR RESULTS? (1 = YES, 2 = NO) ", X

350 PRINT

360 INPUT " ATOMIC NUMBER OF PARENT NUCLEUS, Z = ", Z

370 PRINT

380 INPUT " ELECTRON-CAPTURE ENERGY (keV) = ", G

390 PRINT

400 FOR J = 1 TO 11

410 IF T(1, J + 1) >= Z THEN 430 ELSE 420

420 NEXT J

430 FOR I = 2 TO 9: GOSUB 660: NEXT I

440 IF G > Y(2) THEN R1 = Y(6) * (G - Y(3)) ^ 2 / (G - Y(2)) ^ 2 ELSE R1 = 9.999999E+37

450 IF G > Y(2) THEN R2 = Y(6) * (G - Y(3)) ^ 4 / (G - Y(2)) ^ 4 ELSE R2 = 9.999999E+37

460 IF G > Y(3) THEN R3 = Y(8) ELSE R3 = 9.999999E+37

470 IF G > Y(3) THEN R4 = Y(7) * (G - Y(5)) ^ 2 / (G - Y(3)) ^ 4 * (1000000!) ELSE R4 = 9.999999E+37

480 IF G > 2 * Y(5) THEN R5 = Y(9) ELSE R5 = 9.999999E+37

495 COLOR 14

490 PRINT USING " L(I)/K RATIO (ALLOWED AND NON-UNIQUE F.F.) = ##.##^^^^"; R1

500 PRINT

510 PRINT USING " L(I)/K RATIO (FIRST-FORBIDDEN UNIQUE) = ##.##^^^^"; R2

520 PRINT

530 PRINT USING " L(II)/L(I) RATIO = ##.##^^^^"; R3

540 PRINT

550 PRINT USING " L(III)/L(I) RATIO (F.FORB. UNIQUE ONLY) = ##.##^^^^"; R4

560 PRINT

570 PRINT USING " (M + N + ...)/L RATIO = ##.##^^^^"; R5

580 PRINT

590 PRINT " (NOTE THAT 1.00E+38 ACTUALLY MEANS = INFINITE)"

600 PRINT

610 COLOR 13: PRINT " TO QUIT, HIT CTRL-C": COLOR 15

620 PRINT "****************************************************************"

630 IF X = 1 THEN GOSUB 680

640 GOTO 350

650 END

660 Y(I) = EXP(LOG(T(I, J)) + (Z - T(1, J)) / (T(1, J + 1) - T(1, J)) * (LOG(T(I, J + 1)) - LOG(T(I, J))))

670 RETURN

680 LPRINT "****************************************************************"

690 LPRINT

700 LPRINT USING "Z = ##; EC ENERGY = ##.##^^^^ keV "; Z; G

710 LPRINT

720 LPRINT USING "L(I)/K RATIO (ALLOWED AND NON-UNIQUE F.F.) = ##.##^^^^"; R1

730 LPRINT

740 LPRINT USING "L(I)/K RATIO (FIRST-FORBIDDEN UNIQUE) = ##.##^^^^"; R2

750 LPRINT

760 LPRINT USING "L(II)/L(I) RATIO = ##.##^^^^"; R3

770 LPRINT

780 LPRINT USING "L(III)/L(I) RATIO (F.FORB. UNIQUE ONLY) = ##.##^^^^"; R4

790 LPRINT

800 LPRINT USING "(M + N + ...)/L RATIO = ##.##^^^^"; R5

810 LPRINT

820 LPRINT "(NOTE THAT 1.00E+38 ACTUALLY MEANS = INFINITE)"

830 LPRINT

840 LPRINT "****************************************************************"

850 LPRINT

860 RETURN



