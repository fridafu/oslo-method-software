100 REM    CONVERTE.BAS        SEPTEMBER 16, 1992          J. KANTELE

110 COLOR 14, 5: CLS

120 PRINT "****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CONVERT THE KINETIC ENERGY OR MOMENTUM (Brho) OF "

150 PRINT " ELECTRON IN GIVEN UNITS, TO ENERGY AND MOMENTUM IN SEVERAL "

160 PRINT " UNITS. THE CONVERSION IS BASED ON RELATIVISTIC FORMULAE."

170 PRINT " THE VELOCITY OF THE ELECTRON IS ALSO CALCULATED. "

180 PRINT

190 PRINT "*****************************************************************"

200 PRINT

210 INPUT " DO YOU ENTER ENERGY (1) OR MOMENTUM (2)? CHOICE (1 OR 2) = ", N

220 PRINT

230 ON N GOTO 240, 300

240 PRINT " ENTER THE UNIT OF THE INPUT KINETIC ENERGY: 1 = keV; 2 = MeV; "

250 PRINT

260 INPUT " 3 = mc^2 (= 511 keV). CHOICE = ", N1

270 PRINT

280 INPUT " ENTER THE NUMERICAL VALUE OF THE ENERGY, E = ", E1

290 PRINT : GOTO 420

300 PRINT " ENTER THE UNIT OF THE INPUT MOMENTUM (OR Brho): 1 = Tmm; "

310 PRINT

320 INPUT " 2 = Gauss-cm; 3 = mc (= 511 keV/c); 4 = MeV/c. CHOICE = ", N2

330 PRINT

340 INPUT " ENTER THE NUMERICAL VALUE OF THE MOMENTUM, p = ", P

350 PRINT

360 IF N2 = 1 THEN E = SQR(299.79 ^ 2 * P ^ 2 + 511 ^ 2) - 511: GOTO 450

370 IF N2 = 2 THEN E = SQR(.29979 ^ 2 * P ^ 2 + 511 ^ 2) - 511: GOTO 450

380 IF N2 = 3 THEN P1 = 1.7045 * P: GOTO 390 ELSE 400

390 E = SQR(299.79 ^ 2 * P1 ^ 2 + 511 ^ 2) - 511: GOTO 450

400 IF N2 = 4 THEN P1 = 3.33564 * P

410 E = SQR(299.79 ^ 2 * P1 ^ 2 + 511 ^ 2) - 511: GOTO 450

420 IF N1 = 1 THEN E = E1: GOTO 450

430 IF N1 = 2 THEN E = 1000 * E1: GOTO 450

440 IF N1 = 3 THEN E = 511 * E1

450 BRHO = SQR(E ^ 2 + 2 * 511 * E) / 299.79

460 BRG = 1000 * BRHO

470 BRM = .586679 * BRHO

480 BRMC = .29979 * BRHO

490 V1 = SQR(E ^ 2 + 1022 * E) / (E + 511)

500 V2 = V1 * 2.9979E+08

510 V3 = V1 * 29.979

520 PRINT

530 PRINT "     ELECTRON ENERGY: "

540 PRINT

550 PRINT USING "    ##.###^^^^     keV "; E

560 PRINT USING "    ##.###^^^^     MeV "; E / 1000

570 PRINT USING "    ##.###^^^^     mc^2 (= 511 keV) "; E / 511

580 PRINT

590 PRINT "     ELECTRON MOMENTUM OR Brho: "

600 PRINT

610 PRINT USING "    ##.###^^^^     Tmm "; BRHO

620 PRINT USING "    ##.###^^^^     Gauss-cm "; BRG

630 PRINT USING "    ##.###^^^^     mc (= 511 keV/c) "; BRM

640 PRINT USING "    ##.###^^^^     MeV/c "; BRMC

650 PRINT

660 PRINT USING "     VELOCITY = ##.###^^^^ m/s; ##.### cm/ns; v/c = #.####"; V2; V3; V1

670 PRINT

680 COLOR 15: PRINT "     TO QUIT, HIT CTRL-C ": COLOR 14

690 GOTO 180

700 END



