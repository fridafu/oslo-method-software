100 REM    ETOPOS.BAS         MARCH 15, 1990         J. KANTELE

105 COLOR 12: CLS

110 PRINT "*****************************************************************"

115 PRINT

120 PRINT "PROGRAM TO CALCULATE CAPTURE-TO-POSITRON-EMISSION  RATIOS"

130 PRINT "FOR ALLOWED TRANSITIONS. THE AGREEMENT WITH GOVE AND MARTIN"

140 PRINT "[NDT 10, 206 (1971)] IS GENERALLY MUCH BETTER THAN 8 PER CENT,"

150 PRINT "FOR 10 <~ Z <~ 90 AND 100 keV <~ E <~ 10 MeV."

160 PRINT

170 PRINT "*****************************************************************"

180 PRINT

190 INPUT "ATOMIC NUMBER OF THE DAUGHTER NUCLEUS, Z = ", Z

200 PRINT

210 INPUT "POSITRON END-POINT ENERGY (in keV) E = ", E

220 PRINT

230 FOR K = 1 TO 4

240     FOR L = 1 TO 5

250         READ T(K, L)

260     NEXT L

270 NEXT K

280 DATA 17.9158,1.03634,-.015271,1.67537E-04,-6.95915E-07

290 DATA -4.49856,-.234196,.003486,-4.1263E-05,1.78095E-07

300 DATA .158161,.0279953,-4.7136E-04,5.6809E-06,-2.46707E-08

310 DATA -.0053691,-1.10593E-03,2.054E-05,-2.49394E-07,1.09516E-09

320 FOR I = 1 TO 4

330 GOSUB 420

340 NEXT I

350 K = EXP(C(1) + C(2) * LOG(E) + C(3) * (LOG(E)) ^ 2 + C(4) * (LOG(E)) ^ 3)

360 E1 = 1.069 + .0006069 * Z + 1.7457E-05 * Z ^ 2

370 X = K / E1

380 PRINT USING "K-CAPTURE TO POSITRON-EMISSION RATIO = ##.##^^^^"; X

390 PRINT

400 PRINT USING "EC/POSITRON-EMISSION RATIO = ##.##^^^^ "; K

410 PRINT : GOTO 170

420 C(I) = T(I, 1) + T(I, 2) * Z + T(I, 3) * Z ^ 2 + T(I, 4) * Z ^ 3 + T(I, 5) * Z ^ 4

430 RESTORE

440 RETURN



