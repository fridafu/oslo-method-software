100 REM    DOPPLER.BAS         FEBRUARY 12, 1991         J. KANTELE

110 COLOR 14, 5: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE THE DOPPLER ENERGY-SHIFT OF PHOTONS AND"

150 PRINT " CONVERSION ELECTRONS EMITTED BY MOVING ATOMS/NUCLEI. THE RECOIL "

160 PRINT " EFFECTS ARE IGNORED, SINCE THEY ARE, IN PRACTICAL CASES, "

170 PRINT " NEGLIGIBLE AS COMPARED WITH THE DOPPLER SHIFTS."

180 PRINT " THE CODE ALSO CALCULATES THE DOPPLER BROADENING OF A PHOTON "

190 PRINT " LINE-WIDTH DUE TO THE THERMAL MOTION OF THE ATOMS IN A SOURCE."

200 PRINT

210 PRINT "*****************************************************************"

220 PRINT

230 PRINT " HIT A KEY WHEN READY "

240 IF INKEY$ = "" THEN 240 ELSE 250

250 COLOR 15, 1: CLS

260 LOCATE 3, 10: PRINT "THE ALTERNATIVES FOR DOPPLER-EFFECT CALCULATION ARE:"

270 LOCATE 6, 10: PRINT "1: ENERGY SHIFT OF PHOTON (EXACT)"

280 LOCATE 8, 10: PRINT "2: ENERGY SHIFT OF ELECTRON (EXACT)"

290 LOCATE 10, 10: PRINT "3: DOPPLER BROADENING (THERMAL) OF A LINE"

300 LOCATE 12, 10: PRINT "4: QUIT"

310 LOCATE 18, 10: PRINT "ENTER YOUR CHOICE BY NUMBER (1 TO 4): "

320 V$ = INKEY$: IF VAL(V$) < 1 OR VAL(V$) > 4 THEN 310

330 V = VAL(V$): COLOR 14, 5: CLS : PRINT

340 ON V GOTO 350, 350, 590, 700

350 INPUT " GIVE BETA (=v/c). (IF BETA IS UNKNOWN, HIT ENTER.)  BETA = ", B

360 IF B <> 0 THEN 410

370 PRINT

380 INPUT " ENERGY OF MOVING ATOM (MeV) EA = ", EA

390 PRINT

400 INPUT " MASS-NUMBER OF MOVING ATOM, A = ", A

410 PRINT

420 INPUT " UNSHIFTED PHOTON OR ELECTRON (KINETIC) ENERGY (IN keV) E = ", E

430 PRINT

440 INPUT " ANGLE OF EMISSION (REL. TO ATOM VELOCITY; IN DEG.), THETA = ", THETA

450 PRINT

460 IF B = 0 THEN GOSUB 680

470 IF V = 2 THEN GOTO 530

480 DE = E * (SQR(1 - B ^ 2) / (1 - B * COS(THETA / 57.29578)) - 1)

490 PRINT USING " PHOTON DOPPLER SHIFT DELTA-E = ##.###^^^^ keV"; DE

500 PRINT

510 PRINT USING " SHIFTED PHOTON ENERGY E(full) = ##.####^^^^ keV"; E + DE

520 GOTO 200

530 E0 = E + 511

540 E1 = (SQR(1 - B ^ 2) * E0 + B * 511 * COS(THETA / 57.29578) * SQR(E0 ^ 2 / 511 ^ 2 - 1 + B ^ 2 * (COS(THETA / 57.29578) ^ 2 - E0 ^ 2 / 511 ^ 2))) / (1 - B ^ 2 * COS(THETA / 57.29578) ^ 2) - 511

550 PRINT USING " ELECTRON DOPPLER SHIFT DELTA-E = ##.###^^^^ keV "; E1 - E

560 PRINT

570 PRINT USING " SHIFTED ELECTRON ENERGY E(full) = ##.####^^^^ keV "; E1

580 GOTO 200

590 INPUT " ENERGY OF UNDISTURBED PHOTON LINE (keV) E = ", E

600 PRINT

610 INPUT " MASS-NUMBER OF EMITTING ATOM/NUCLEUS A = ", A

620 PRINT

630 INPUT " TEMPERATURE (CENTIGRADES) T(C) = ", T1

640 DB = 2 * E * SQR(2 * (8.6174E-11) * (273.15 + T1) * LOG(2) / (A * 931.5)) * 1000

650 PRINT

660 PRINT USING " THERMAL DOPPLER BROADENING (NO SHIFT) DELTA-E = ##.##^^^^ eV"; DB

670 GOTO 200

680 B = SQR(EA ^ 2 + 2 * EA * A * 931.5) / (EA + A * 931.5)

690 RETURN

700 END



