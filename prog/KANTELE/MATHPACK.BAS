10  REM      MATHPACK.BAS         APRIL 27, 1993           J. KANTELE

100 COLOR 14, 4: CLS : PRINT

110 PRINT " SOME SIMPLE MATHEMATICS PROGRAMS AND FORMULAE: "

120 PI = 3.1415927#: RAD = 57.29578: EE = 2.718282

130 PRINT

140 PRINT "  1. PRIME FACTORS OF INTEGERS     8. SOLUTIONS OF EQUATIONS "

150 PRINT "  2. PERMUTATIONS/COMBINATIONS     9. ROOTS OF POLYNOMIALS "

160 PRINT "  3. TRIANGLE PARTS               10. GEOMETRIC MEAN "

170 PRINT "  4. SPIRAL                       11. LIST OF INTEGRALS "

180 PRINT "  5. ARITHMETIC/GEOMETRIC SERIES  12. NUMERICAL INTEGRATION "

190 PRINT "  6. SERIES EXPANSIONS            13. PROBABILITY DISTRIBUTION "

200 PRINT "  7. LINEAR REGRESSION            14. QUIT "

210 PRINT

220 INPUT " CHOOSE PROGRAM BY NUMBER (1...14), N = ", N

230 PRINT : IF N = 14 THEN 9930

240 ON N GOTO 300, 800, 1200, 2000, 2500, 3000, 5000, 5500, 6500, 7500, 8000, 9000, 9500

300 COLOR 11, 6: CLS : PRINT

310 PRINT " PRIME FACTORS OF INTEGERS"

320 PRINT

330 REM  9887 * 9803 = 96922261; 157 * 1021 = 160297; 3203 * 3301 = 10573103

340 PRINT " (ENTER 0 TO END PROGRAM)"

350 PRINT : DEFDBL A-Z

360 PRINT "********************************************************************"

370 PRINT : COLOR 11, 6

380 INPUT " NUMBER = ", Z

390 REM   ENTER PROGRAM?

400 IF Z = 0 THEN 710

410 REM - THE SIGN Of THE NUMBER IS ALWAYS A FACTOR

420 PRINT : COLOR 15

430 PRINT " THE PRIME FACTORS ARE: "

440 PRINT : T = 1: R = 1

450 PRINT SGN(Z)

460 COLOR 11

470 REM - USE ABSOLUTE VALUE FOR CALCULATION

480 Z = ABS(Z)

490 X = Z

500 REM - LOOP TO TEST ALL INTEGERS (2 THROUGH Z) AS PRIME FACTORS

510 REM - INTEGERS SQR(Z) THRU Z WILL HAVE NO NEW FACTORS

520 FOR I = 2 TO Z

530 S = 0

540 IF Z / I <> INT(Z / I) THEN 590

550 Z = Z / I

560 S = S + 1

570 GOTO 540

580 REM - FIND A PRlME FACTOR? IF YES, PRINT

590 IF S = 0 THEN 670

600 REM - PRINT FACTORS WITH EXPONENTS; I^S = I TO THE S POWER

610 COLOR 15

620 PRINT I; "^"; S

630 COLOR 14

640 IF S > 0 THEN R = I ^ S ELSE R = 1

650 T = T * R

660 IF T = X THEN 675

670 NEXT I

675 PRINT

680 PRINT " WANT TO CALCULATE ANOTHER CASE (1), RETURN TO THE MAIN MENU (2),"

690 INPUT " OR TO QUIT (3)? YOUR CHOICE = ", Q1

700 ON Q1 GOTO 370, 100, 710

710 END



800 COLOR 14, 5: CLS : PRINT

810 PRINT " PERMUTATIONS AND COMBINATIONS"

820 PRINT

830 PRINT " (ENTER 0 TO END PROGRAM)"

840 PRINT

850 INPUT " TOTAL NUMBER OF OBJECTS = ", N

860 IF N = 0 THEN 1120

870 PRINT

880 INPUT " SIZE OF SUBGROUP = ", D

890 IF D <= N THEN 930

900 PRINT " SUBGROUP TOO LARGE "

910 PRINT

920 GOTO 850

930 P = 1

940 C = 1

950 FOR I = N - D + 1 TO N

960 IF (9.9E+37) / I >= P THEN 1000

970 PRINT

980 PRINT " MORE THAN 9.9E+37 PERMUTATIONS "

990 GOTO 1120

1000 P = P * I

1010 NEXT I

1020 FOR J = 2 TO D

1030 C = C * J

1040 NEXT J

1050 PRINT : COLOR 11

1060 PRINT USING " ##.###^^^^ PERMUTATIONS"; P

1070 PRINT

1080 PRINT USING " ##.###^^^^ COMBINATIONS"; P / C

1090 PRINT : COLOR 14

1100 PRINT " WANT TO CALCULATE ANOTHER CASE (1), RETURN TO THE MAIN MENU (2),"

1110 INPUT " OR TO QUIT (3)? YOUR CHOICE = ", Q2

1115 ON Q2 GOTO 800, 100, 1120

1120 END



1200 REM   TRIANGLE.BAS       JANUARY 13, 1993      (MODIFIED BY J. KANTELE)

1210 COLOR 14, 5: CLS : PRINT

1220 PRINT " PROGRAM TO CALCULATE ALL PARTS OF A TRIANGLE, STARTING FROM THREE "

1230 PRINT " KNOWN ONES. THERE ARE 5 WAYS TO DEFINE A TRIANGLE UNIQUELY. "

1240 PRINT

1250 PI = 3.14159265#: R = 57.29578

1260 PRINT " ENTER NUMBER OF PROBLEM TYPE ACCORDING TO THE KNOWN PARTS OF THE "

1270 PRINT " TRIANGLE, WHERE A = ANGLE (degs.), S = LENGTH OF SIDE (cm). "

1280 PRINT : COLOR 15

1290 PRINT " PROBLEM TYPES: 1 = ASA, 2 = SAS, 3 = AAS, 4 = SSA, 5 = SSS, 6 = END "

1300 PRINT : COLOR 14

1310 PRINT " DATA MUST BE ENTERED IN THE ORDER THEY APPEAR IN THE TRIANGLE, "

1320 PRINT " FOR EXAMPLE, COUNTERCLOCKWISE. "

1330 PRINT

1340 INPUT " ENTER PROBLEM TYPE = ", ZT

1350 ON ZT GOTO 1390, 1450, 1520, 1560, 1650, 1850

1360 DEF FNA (X) = ATN(X / SQR(1 - X ^ 2)) * R

1370 DEF FNB (X) = ATN(SQR(1 - X ^ 2) / X) * R

1380 PRINT

1390 PRINT " ENTER ANGLE, SIDE, ANGLE";

1400 INPUT " = ", A(1), S(3), A(2)

1410 A(3) = PI * R - A(1) - A(2)

1420 S(1) = S(3) * SIN(A(1) / R) / SIN(A(3) / R)

1430 S(2) = S(3) * SIN(A(2) / R) / SIN(A(3) / R)

1440 GOTO 1700

1450 PRINT " ENTER SIDE, ANGLE, SIDE";

1460 INPUT " = ", S(3), A(1), S(2)

1470 S(1) = SQR(S(3) ^ 2 + S(2) ^ 2 - 2 * S(3) * S(2) * COS(A(1) / R))

1480 A(2) = SIN(A(1) / R) / S(1) * S(2) * R

1490 A(2) = FNA(A(2) / R)

1500 A(3) = PI * R - A(1) - A(2)

1510 GOTO 1700

1520 PRINT " ENTER ANGLE, ANGLE, SIDE";

1530 INPUT " = ", A(3), A(2), S(3)

1540 A(1) = PI * R - A(2) - A(3)

1550 GOTO 1420

1560 PRINT " ENTER SIDE, SIDE, ANGLE";

1570 INPUT " = ", S(1), S(2), A(1)

1580 T = S(2) * SIN(A(1) / R)

1590 IF S(1) < T THEN 1850

1600 S(3) = SQR(S(2) ^ 2 - T ^ 2)

1610 IF S(1) <= T THEN 1640

1620 Y = SQR(S(1) ^ 2 - T ^ 2)

1630 S(3) = S(3) + Y

1640 GOTO 1480

1650 PRINT " ENTER SIDE, SIDE, SIDE";

1660 INPUT " = ", S(1), S(2), S(3)

1670 A(1) = (S(2) ^ 2 + S(3) ^ 2 - S(1) ^ 2) / 2 / S(2) / S(3) * R

1680 A(1) = FNB(A(1) / R)

1690 GOTO 1480

1700 PRINT : COLOR 11

1710 FOR I = 1 TO 3

1720 IF A(I) < 0 THEN 1830

1730 SO(I) = INT(S(I) * 1000 + .5) / 1000

1740 AO(I) = INT(A(I) * 1000 + .5) / 1000

1750 PRINT USING " SIDE #           = ##.###^^^^ cm "; I; SO(I)

1760 PRINT USING " OPPOSITE ANGLE   = ##.###^^^^ deg."; AO(I)

1770 PRINT

1780 NEXT I

1790 U = .5 * (SO(1) + SO(2) + SO(3))

1800 AREA = SQR(U * (U - SO(1)) * (U - SO(2)) * (U - SO(3)))

1810 PRINT USING " AREA OF TRIANGLE = ##.###^^^^ cm^2 "; AREA

1820 GOTO 1850

1830 PRINT

1840 PRINT " NO SOLUTION "

1850 PRINT : COLOR 14

1860 PRINT " WANT TO CALCULATE ANOTHER CASE (1), RETURN TO THE MAIN MENU (2),"

1870 INPUT " OR TO QUIT (3)? YOUR CHOICE = ", Q3

1880 ON Q3 GOTO 1240, 100, 1890

1890 END





2000 REM     SPIRAL.BAS            DECEMBER 26, 1992          J. KANTELE

2010 COLOR 14, 5: CLS

2020 PRINT

2030 PRINT " PROGRAM TO DESCRIBE CIRCULAR SPIRAL IN THE Z DIRECTION. THE RADIUS "

2040 PRINT " OF THE PROJECTION CIRCLE IS = R, AND THE SPIRAL STARTS AT THE POINT "

2050 PRINT " (R,0,0), WITH TAKE-OFF ANGLE OF THETA (RADS.) FROM THE XY-PLANE. "

2060 PRINT " THE SPIRAL CAN BE THOUGHT OF AS BEING FORMED ON THE SURFACE OF ROTAT- "

2070 PRINT " ING CYLINDER, WHERE A POINT IS MOVING IN THE DIRECTION OF THE AXIS. "

2080 PRINT " NOTE: SOMETIMES THETA = ANGLE BETWEEN THE SPIRAL AND THE XZ-PLANE "

2090 PRINT

2100 PRINT " USING PARAMETER T (= AZIMUTH ANGLE, RADS.) REPRESENTATION, WE HAVE :"

2110 PRINT

2120 PRINT " X = R * COS(T); Y = R * SIN(T); Z = BT, WHERE B = CONSTANT "

2130 PRINT

2140 INPUT " GIVE RADIUS (mm) R = ", R

2150 PRINT : PI = 3.141593

2160 INPUT " AZIMUTH (ROTATION ON THE XY PLANE) ANGLE T (IN DEGS.) T = ", T

2170 PRINT

2180 PRINT " DO YOU GIVE THE RISING ANGLE THETA (1) OR THE RISE Z(0) = H (2)? "

2190 INPUT " THE RISE H = THE DISTANCE BETWEEN TWO LOOPS. YOUR CHOICE = ", N

2200 PRINT : ON N GOTO 2210, 2240

2210 INPUT " RISING (OR TAKE-OFF) ANGLE THETA (IN DEGS.) = ", THETA

2220 B = R * TAN(THETA / 57.29578)

2230 H = 2 * PI * B: GOTO 2270

2240 INPUT " RISE (FOCAL LENGTH) OF SPIRAL (=2*Pi*B) = H (mm) = ", H

2250 THETA = ATN(H / (2 * PI * R)) * 57.29578

2260 B = H / 2 / PI

2270 Z = B * T / 57.29578

2280 X = R * COS(T / 57.29578): Y = R * SIN(T / 57.29579)

2290 PRINT : COLOR 11

2300 PRINT USING " RISE OF SPIRAL ON FULL LOOP Z(0) = H = ##.###^^^^ mm "; H

2310 PRINT

2320 PRINT USING " RISING (TAKE-OFF) ANGLE OF SPIRAL, THETA = ##.###^^^^ DEG."; THETA

2330 PRINT

2340 PRINT USING " Z-VALUE (HEIGHT) DEFINED BY T IS  Z = ##.###^^^^ mm "; Z

2350 PRINT

2360 PRINT USING " X-VALUE DEFINED BY T IS  Z = ##.###^^^^ mm "; X

2370 PRINT

2380 PRINT USING " Y-VALUE DEFINED BY T IS  Z = ##.###^^^^ mm "; Y

2390 PRINT : COLOR 14

2400 PRINT " WANT TO CALCULATE ANOTHER CASE (1), RETURN TO THE MAIN MENU (2),"

2410 INPUT " OR TO QUIT (3)? YOUR CHOICE = ", Q

2420 ON Q GOTO 2130, 100, 2430

2430 END



2500 REM   ARGEOSER.BAS            DECEMBER 17, 1992         J. KANTELE

2510  COLOR 14, 5: CLS

2520 PRINT

2530 INPUT " WANT TO CALCULATE ARITHMETIC (1) OR GEOMETRIC SERIES (2)? CHOICE (1 OR 2) = ", M

2540 ON M GOTO 2550, 2740

2550 CLS : PRINT

2560 PRINT " THE DIFFERENCE OF SUCCESSIVE TERMS IN ARITHMETIC SERIES IS CONSTANT: "

2570 PRINT

2580 PRINT " A + (A + D) +  (A + 2D) + (A + 3D) + ....."

2590 PRINT

2600 PRINT " THE SUM OF N FIRST TERMS IS  S(N) = N * A + ´ * N * (N - 1) * D "

2610 PRINT

2620 INPUT " GIVE THE VALUE OF THE FIRST TERM, A = ", A

2630 PRINT

2640 INPUT " THE DIFFERENCE OF SUCCESSIVE TERMS, D = ", D

2650 PRINT

2660 INPUT " THE NUMBER OF TERMS, N = ", N

2670 PRINT : COLOR 11

2680 S = N * A + .5 * N * (N - 1) * D

2690 PRINT USING " THE SUM OF THE FIRST #### TERMS IS  S(N) = ##.###^^^^"; N; S

2700 PRINT : COLOR 14

2710 PRINT " WANT TO CALCULATE ANOTHER CASE (1), RETURN TO THE MAIN MENU (2),"

2720 INPUT " OR TO QUIT (3)? YOUR CHOICE = ", Q

2730 ON Q GOTO 2500, 100, 2735

2735 END





2740  COLOR 14: CLS

2750 PRINT

2760 PRINT " THE RATIO OF SUCCESSIVE TERMS IN A GEOMETRIC SERIES IS CONSTANT:"

2770 PRINT

2780 PRINT " A + AQ + AQ^2 + AQ^3 +....."

2790 PRINT

2800 PRINT " THE SERIES CONVERGES, IF |Q| < 1. "

2810 PRINT

2820 PRINT " THE SUM OF THE FIRST N TERMS IS S(N) = A * (1 - Q^N)/(1 - Q) "

2830 PRINT

2840 INPUT " GIVE THE FIRST TERM, A = ", A

2850 PRINT

2860 INPUT " THE RATIO OF SUCCESSIVE TERMS  A(N + 1)/A(N) = Q = ", Q

2870 PRINT

2880 INPUT " THE NUMBER OF THE TERMS (IF INFINITE, GIVE 30,000), N = ", N

2890 PRINT : COLOR 11

2900 S = A * (1 - Q ^ N) / (1 - Q)

2910 PRINT USING " THE SUM OF THR FIRST #### TERMS, S(N) = ##.###^^^^"; N; S

2920 PRINT : COLOR 14

2930 PRINT " WANT TO CALCULATE ANOTHER CASE (1), RETURN TO THE MAIN MENU (2),"

2940 INPUT " OR TO QUIT (3)? YOUR CHOICE = ", Q5

2950 ON Q5 GOTO 2500, 100, 2960

2960 END



3000 REM     SERIES.BAS          DECEMBER 16, 1992           J. KANTELE

3010 PI = 3.1415927#: RAD = 57.29578: EE = 2.718282

3020 COLOR 14, 5: CLS : PRINT

3030 PRINT " PROGRAM FOR COMPUTING APPROXIMATE SERIES EXPANSIONS AND "

3040 PRINT " ACCURATE VALUES OF SOME FUNCTIONS. "

3050 PRINT

3060 PRINT " LIST OF SOME COMMON SERIES EXPANSIONS OF FUNCTIONS: "

3070 PRINT

3080 PRINT " 1.  exp(x) = 1 + x + x^2/2! + x^3/3! + ...."

3090 PRINT

3100 PRINT " 2.  sin(x) = x - x^3/3! + x^5/5! - ...."

3110 PRINT

3120 PRINT " 3.  cos(x) = 1 - x^2/2! + x^4/4! - ...."

3130 PRINT

3140 PRINT " 4.  (1 + x)^m = 1 + mx + m(m - 1)x^2/2! + .. + m(m-1)..(m-k+1)x^k/k! +.."

3150 PRINT

3160 PRINT " 5.  ln(1 + x) = x - x^2/2 + x^3/3 - x^4/4 + ...."

3170 PRINT

3180 PRINT " 6.  arctan(x) = x - x^3/3 + x^5/5 - x^7/7 + ...."

3190 PRINT

3200 PRINT " 7.  arcsin(x) = x + x^3/6 + 3x^5/40 + 5x^7/112 + ...."

3210 PRINT

3220 PRINT " 8. QUIT"

3230 PRINT

3240 INPUT " CHOOSE THE FUNCTION BY NUMBER (1...8) = ", N92

3250 IF N92 = 8 THEN 4540

3260 PRINT : COLOR 15

3270 PRINT " THE NUMBER OF TERMS CAN BE 2, 3, OR 4 (IN THE arcsin CASE, "

3280 PRINT " THE PROGRAM ALWAYS COMPUTES  THE 3- AND 5-TERM CASES). "

3290 PRINT : IF N92 = 7 THEN 3330

3300 COLOR 14

3310 INPUT " GIVE THE NUMBER OF TERMS IN CASES 1...6 (I.E., 2..4), N = ", N93

3320 PRINT : IF ABS(N93 - 3) > 1 THEN 3290

3330 COLOR 15

3340 IF (1 < N92) * (N92 < 4) THEN 3410

3350 PRINT " THE MOST ACCURATE VALUES ARE OBTAINED WHEN |x| IS SMALL. "

3360 PRINT

3370 PRINT " LIMITATIONS IN SOME CASES: 4 ->  |x| < 1;  5, 6, 7 ->  -1 < x <= 1."

3380 PRINT : COLOR 14

3390 INPUT " X = ", X1

3400 PRINT : GOTO 3430

3410 INPUT " ENTER THE ANGLE IN DEGREES, X = ", X1

3420 PRINT

3430 IF (1 < N92) * (N92 < 4) THEN X = X1 / 57.29578 ELSE X = X1

3440 ON N92 GOTO 3450, 3600, 3750, 3900, 4070, 4220, 4370, 4540

3450 IF N93 = 2 THEN Y = 1 + X: GOTO 3470

3460 IF N93 = 3 THEN Y = 1 + X + X ^ 2 ELSE Y = 1 + X + X ^ 2 + X ^ 3 / 6

3470 COLOR 11

3480 PRINT USING " APPROX. VALUE   exp(##.###^^^^) = ##.###^^^^"; X1; Y

3490 PRINT

3500 PRINT USING " ACCURATE VALUE  exp(##.###^^^^) = ##.###^^^^"; X1; EXP(X)

3510 PRINT : COLOR 14

3520 PRINT " THE ALTERNATIVES FOR CONTINUATION ARE:"

3530 PRINT

3540 PRINT "  1. THE SAME PROGRAM AGAIN "

3550 PRINT "  2. OTHER SERIES EXPANSIONS "

3560 PRINT "  3. RETURN TO THE MAIN MENU "

3570 PRINT

3580 INPUT " YOUR CHOICE (1, 2 OR 3) = ", N

3590 ON N GOTO 3260, 3050, 100

3600 IF N93 = 2 THEN Y = X - X ^ 3 / 6: GOTO 3630

3610 IF N93 = 3 THEN Y = X - X ^ 3 / 6 + X ^ 5 / 120 ELSE Y = X - X ^ 3 / 6 + X ^ 5 / 120 - X ^ 7 / 5040

3620 COLOR 11

3630 PRINT USING " APPROX. VALUE   sin(##.###^^^^) = ##.###^^^^"; X1; Y

3640 PRINT

3650 PRINT USING " ACCURATE VALUE  sin(##.###^^^^) = ##.###^^^^"; X1; SIN(X)

3660 PRINT : COLOR 14

3670 PRINT " THE ALTERNATIVES FOR CONTINUATION ARE:"

3680 PRINT

3690 PRINT "  1. THE SAME PROGRAM AGAIN "

3700 PRINT "  2. OTHER SERIES EXPANSIONS "

3710 PRINT "  3. RETURN TO THE MAIN MENU "

3720 PRINT

3730 INPUT " YOUR CHOICE (1, 2 OR 3) = ", N

3740 ON N GOTO 3260, 3050, 100

3750 IF N93 = 2 THEN Y = 1 - X ^ 2 / 2: GOTO 3780

3760 IF N93 = 3 THEN Y = 1 - X ^ 2 / 2 + X ^ 4 / 24 ELSE Y = 1 - X ^ 2 / 2 + X ^ 4 / 24 - X ^ 6 / 720

3770 COLOR 11

3780 PRINT USING " APPROX. VALUE   cos(##.###^^^^) = ##.###^^^^"; X1; Y

3790 PRINT

3800 PRINT USING " ACCURATE VALUE  cos(##.###^^^^) = ##.###^^^^"; X1; COS(X)

3810 PRINT : COLOR 14

3820 PRINT " THE ALTERNATIVES FOR CONTINUATION ARE:"

3830 PRINT

3840 PRINT "  1. THE SAME PROGRAM AGAIN "

3850 PRINT "  2. OTHER SERIES EXPANSIONS "

3860 PRINT "  3. RETURN TO THE MAIN MENU "

3870 PRINT

3880 INPUT " YOUR CHOICE (1, 2 OR 3) = ", N

3890 ON N GOTO 3260, 3050, 100

3900 INPUT " ENTER THE EXPONENT (REAL NUMBER)  m = ", M

3910 PRINT

3920 IF N93 = 2 THEN Y = 1 + M * X: GOTO 3940

3930 IF N93 = 3 THEN Y = 1 + M * X + M * (M - 1) * X ^ 2 ELSE Y = 1 + M * X + M * (M - 1) * X ^ 2 / 2 + M * (M - 1) * (M - 2) * X ^ 3 / 6

3940 COLOR 11

3950 PRINT USING " APPROX. VALUE   (1 + ##.###^^^^)^##.###^^^^ = ##.###^^^^"; X1; M; Y

3960 Y1 = (1 + X) ^ M

3970 PRINT USING " ACCURATE VALUE  (1 + ##.###^^^^)^##.###^^^^ = ##.###^^^^"; X1; M; Y1

3980 PRINT : COLOR 14

3990 PRINT " THE ALTERNATIVES FOR CONTINUATION ARE:"

4000 PRINT

4010 PRINT "  1. THE SAME PROGRAM AGAIN "

4020 PRINT "  2. OTHER SERIES EXPANSIONS "

4030 PRINT "  3. RETURN TO THE MAIN MENU "

4040 PRINT

4050 INPUT " YOUR CHOICE (1, 2 OR 3) = ", N

4060 ON N GOTO 3260, 3050, 100

4070 IF N93 = 2 THEN Y = X - X ^ 2 / 2: GOTO 4100

4080 IF N93 = 3 THEN Y = X - X ^ 2 / 2 + X ^ 3 / 3 ELSE Y = X - X ^ 2 / 2 + X ^ 3 / 3 - X ^ 4 / 4

4090 COLOR 11

4100 PRINT USING " APPROX. VALUE   ln(1 + ##.###^^^^) = ##.###^^^^"; X1; Y

4110 Y1 = LOG(1 + X1)

4120 PRINT USING " ACCURATE VALUE  ln(1 + ##.###^^^^) = ##.###^^^^"; X1; Y1

4130 PRINT : COLOR 14

4140 PRINT " THE ALTERNATIVES FOR CONTINUATION ARE:"

4150 PRINT

4160 PRINT "  1. THE SAME PROGRAM AGAIN "

4170 PRINT "  2. OTHER SERIES EXPANSIONS "

4180 PRINT "  3. RETURN TO THE MAIN MENU "

4190 PRINT

4200 INPUT " YOUR CHOICE (1, 2 OR 3) = ", N

4210 ON N GOTO 3260, 3050, 100

4220 IF N93 = 2 THEN Y = X - X ^ 3 / 3: GOTO 4250

4230 IF N93 = 3 THEN Y = X - X ^ 3 / 3 + X ^ 5 / 5 ELSE Y = X - X ^ 3 / 3 + X ^ 5 / 5 - X ^ 7 / 7

4240 COLOR 11

4250 PRINT USING " APPROX. VALUE   arctan(##.###^^^^) = ##.###^^^^"; X1; Y * RAD

4260 Y1 = ATN(X1) * RAD

4270 PRINT USING " ACCURATE VALUE  arctan(##.###^^^^) = ##.###^^^^"; X1; Y1

4280 PRINT : COLOR 14

4290 PRINT " THE ALTERNATIVES FOR CONTINUATION ARE:"

4300 PRINT

4310 PRINT "  1. THE SAME PROGRAM AGAIN "

4320 PRINT "  2. OTHER SERIES EXPANSIONS "

4330 PRINT "  3. RETURN TO THE MAIN MENU "

4340 PRINT

4350 INPUT " YOUR CHOICE (1, 2 OR 3) = ", N

4360 ON N GOTO 3260, 3050, 100

4370 Y3 = X + X ^ 3 / 6 + 3 * X ^ 5 / 40

4380 Y5 = Y3 + 5 * X ^ 7 / 112 + 35 * X ^ 9 / 1152

4390 COLOR 11

4400 Y31 = Y3 * RAD

4410 Y51 = Y5 * RAD

4420 PRINT USING " APPROX. VALUE (3 terms)  arcsin(##.###^^^^) = ##.###^^^^ deg"; X1; Y31

4430 PRINT USING " APPROX. VALUE (5 terms)  arcsin(##.###^^^^) = ##.###^^^^ deg"; X1; Y51

4440 PRINT : COLOR 14

4450 PRINT : COLOR 14

4460 PRINT " THE ALTERNATIVES FOR CONTINUATION ARE:"

4470 PRINT

4480 PRINT "  1. THE SAME PROGRAM AGAIN "

4490 PRINT "  2. OTHER SERIES EXPANSIONS "

4500 PRINT "  3. RETURN TO THE MAIN MENU "

4510 PRINT

4520 INPUT " YOUR CHOICE (1, 2 OR 3) = ", N

4530 ON N GOTO 3260, 3050, 100

4540 END



5000 COLOR 14, 5: CLS : PRINT

5010 PRINT " PROGRAM FOR LINEAR REGRESSION "

5020 PRINT

5030 INPUT " NUMBER OF POINTS (AT LEAST 3 PTS.) = ", N

5040 PRINT

5050 J = 0

5060 K = 0

5070 L = 0

5080 M = 0

5090 R2 = 0

5100 FOR I = 1 TO N

5110 PRINT " X,Y OF POINT"; I;

5120 INPUT " = ", X, Y

5130 J = J + X

5140 K = K + Y

5150 L = L + X ^ 2

5160 M = M + Y ^ 2

5170 R2 = R2 + X * Y

5180 NEXT I

5190 B = (N * R2 - K * J) / (N * L - J ^ 2)

5200 A = (K - B * J) / N

5210 PRINT

5220 PRINT " F(X) = "; A; " + ("; B; "* X )"

5230 J = B * (R2 - J * K / N)

5240 M = M - K ^ 2 / N

5250 K = M - J

5260 PRINT

5270 R2 = J / M

5280 PRINT : COLOR 11

5290 PRINT USING " COEFFICIENT OF DETERMINATION = ##.###^^^^ "; R2

5300 PRINT USING " COEFFICIENT OF CORRELATlON   = ##.###^^^^ "; SQR(R2)

5310 PRINT USING " STANDARD ERROR OF ESTIMATE   = ##.###^^^^ "; SQR(K / (N - 2))

5320 PRINT : COLOR 14

5330 PRINT " INTERPOLATION FOR A VALUE OF X: "

5335 PRINT " (ENTER X = 0 TO RETURN TO MAIN MENU) "

5340 PRINT : COLOR 11

5350 INPUT " X = ", X

5360 IF X = 0 THEN 5410

5370 PRINT USING " Y = ##.###^^^^ "; A + B * X

5380 PRINT : COLOR 14

5390 PRINT "****************************************************************"

5400 PRINT : GOTO 5350

5410 GOTO 100



5500 REM    EQUATNS.BAS      JULY 3, 1990     (MODIFIED BY J. KANTELE)

5510 COLOR 14, 5: CLS : PRINT

5520 PRINT "****************************************************************"

5530 PRINT " PROGRAM TO SOLVE SIMULTANEOUS LINEAR EQUATIONS (MAX. 30)"

5540 PRINT

5550 PRINT " THE EQUATION FORMS ARE: A1*X1 + A2*X2 + ...+ AN*XN = CONST.1 = C1 "

5560 PRINT "                         B1*X1 + B2*X2 + ...+ BN*XN = CONST.2 = C2 "

5570 PRINT "                         - - - - - - - - - - - - - - -"

5580 PRINT " THE INPUT VALUES ARE: A1,A2,...,AN,C1; B1,B2,...,BN,C2; ETC."

5590 PRINT "****************************************************************"

5600 PRINT

5610 DIM M(30, 30), P(30), L(30)

5620 REM

5630 INPUT " ENTER NUMBER OF EQUATIONS = ", Z

5640 PRINT

5650 FOR E = 1 TO Z: FOR D = 1 TO Z

5660 PRINT USING " EQUATION NO. #; COEFFICIENT NO. # "; E; D;

5670 INPUT "= ", M(E, D)

5680 NEXT D: PRINT USING "            CONSTANT FOR EQN NO. # "; E;

5690 INPUT "= ", L(E): NEXT E

5700 PRINT

5710 FOR E = 1 TO Z - 1

5720 L = ABS(M(E, E)): B = E

5730 FOR D = E + 1 TO Z

5740 IF ABS(M(D, E)) < L THEN 5760

5750 B = D: L = ABS(M(D, E))

5760 NEXT D

5770 IF L = 0 THEN 6030

5780 IF B = E THEN 5870

5790 FOR D = 1 TO Z

5800 G = M(B, D)

5810 M(B, D) = M(E, D)

5820 M(E, D) = G

5830 NEXT D

5840 V = L(B)

5850 L(B) = L(E)

5860 L(E) = V

5870 FOR D = E + 1 TO Z

5880 T = M(D, E) / M(E, E)

5890 FOR C = E + 1 TO Z

5900 M(D, C) = M(D, C) - T * M(E, C)

5910 NEXT C

5920 L(D) = L(D) - T * L(E)

5930 NEXT D

5940 NEXT E

5950 IF M(Z, Z) = 0 THEN 6030

5960 E = Z - 1: P(Z) = L(Z) / M(Z, Z)

5970 U = 0: FOR D = E + 1 TO Z

5980 U = U + M(E, D) * P(D)

5990 NEXT D

6000 P(E) = (L(E) - U) / M(E, E)

6010 E = E - 1: IF E > 0 THEN 5970

6020 GOTO 6050

6030 PRINT : PRINT " THE EQUATIONS ARE UNSOLVABLE!"

6040 GOTO 6090

6050 COLOR 11

6060 FOR E = 1 TO Z

6070 PRINT USING " VARIABLE NO. # =##.###^^^^ "; E; P(E)

6080 NEXT E: COLOR 14: PRINT

6090 INPUT " TRY AGAIN (1=YES, 2=NO)"; N

6100 PRINT

6110 IF N = 1 THEN GOTO 5590

6120 GOTO 100



6500 COLOR 14, 5: CLS : PRINT

6510 PRINT " ROOTS OF POLYNOMIALS: HALF-INTERVAL SEARCH: "

6520 PRINT " THE PROGRAM FIRST CONDUCTS A RANDOM SEARCH WITHIN THE GIVEN "

6530 PRINT " INTERVAL FOR TWO POINTS OF OPPOSITE SIGNS. IF A CHANGE IS FOUND,"

6540 PRINT " THEN THE ROOT IS CALCULATED. IF NOT, ANOTHER INTERVAL IS ASKED FOR."

6550 PRINT

6560 PRINT " ENTER YOUR POLYNOMIAL AT LINE 6570 (DEF FNR(X) = 'FUNCTION') "

6570 DEF FNR (X) = 2 * X ^ 6 - 4 * X ^ 4 - 2.5 * X ^ 2 - X + .5

6580 PRINT " (TO END SEARCH ENTER 0,0)"

6590 PRINT

6600 REM - ESTABLISH lNTERVAL OF RANDOM SEARCH

6610 PRINT " INTERVAL (LOWER, UPPER LIMIT)";

6620 INPUT A, B

6630 REM - TEST FOR USABLE LIMITS ENTERED

6640 IF A <> B THEN 6690

6650 IF A = 0 THEN 7170

6660 REM - END PROGRAM?

6670 PRINT "--INTERVAL LIMITS CANNOT BE EQUAL--"

6680 GOTO 6610

6690 IF A < B THEN 6720

6700 PRINT "--LOWER LIMIT MUST BE ENTERED FIRST--"

6710 GOTO 6610

6720 A1 = SGN(FNR(A))

6730 B1 = SGN(FNR(B))

6740 REM - TEST FOR ROOT AT EITHER LIMIT

6750 IF A1 * B1 = 0 THEN 7080

6760 REM - TEST FOR OPPOSITE SIGNS AT INTERVAL LIMITS

6770 IF A1 * B1 < 0 THEN 6940

6780 REM - LOOP TO SEARCH 1000 NUMBERS FOR OPPOSITE SIGNS IN FUNCTION

6790 FOR I = 1 TO 1000

6800 X = A + RND(2) * (B - A)

6810 X1 = SGN(FNR(X))

6820 REM - TEST FOR ROOT AT RANDOM NUMBER; IF YES, END SEARCH, PRINT

6830 IF X1 = 0 THEN 7130

6840 REM - TEST FOR OPPOSITE SIGNS AT RANDOM NUMBER AND LOWER LIMIT

6850 IF A1 * X1 < 0 THEN 6910

6860 REM - TRY ANOTHER RANDOM NUMBER

6870 NEXT I

6880 PRINT " NO CHANGE OF SIGN FOUND"

6890 GOTO 6610

6900 REM - CHANGE OF SIGN FOUND; CALCULATE ROOT

6910 B = X

6920 REM - STORE POSITIVE POINT IN D(3), NEGATIVE POINT IN D(1)

6930 REM - D(1) AND D(3) BECOME INTERVAL LIMITS

6940 D(2 + A1) = A

6950 D(2 - A1) = B

6960 REM - CALCULATE MIDPOINT BETWEEN THE TWO LIMITS

6970 Y = (D(1) + D(3)) / 2

6980 Y1 = SGN(FNR(Y))

6990 REM - TEST FOR ROOT AT MIDPOINT

7000 IF Y1 = 0 THEN 7120

7010 REM - GET A NEW LIMIT TO CLOSE IN ROOT

7020 D(2 + Y1) = Y

7030 REM - TEST FOR A VALUE CLOSE ENOUGH TO ZERO TO ASSUME A ROOT

7040 IF ABS(D(1) - D(3)) / ABS(D(1) + ABS(D(3))) < .00005 THEN 7120

7050 REM - RETEST WITH NEW LIMITS

7060 GOTO 6970

7070 REM - ROOT AT AN INTERVAL LIMIT; FIND WHICH LIMIT, PRINT

7080 IF A1 = 0 THEN 7110

7090 Y = B

7100 GOTO 7120

7110 Y = A

7120 COLOR 11: PRINT

7130 PRINT " ROOT = "; Y

7140 COLOR 14: PRINT

7150 REM - RESTART PROGRAM

7160 GOTO 6610

7170 GOTO 100



7500 COLOR 14, 5: CLS : PRINT

7510 PRINT " GEOMETRIC MEAN AND DEVIATION"

7520 PRINT

7530 PRINT " (TO END PROGRAM ENTER 0 OBSERVATIONS) "

7540 PRINT

7550 INPUT " NUMBER OF OBSERVATIONS = ", N

7560 PRINT : Q = 0

7570 IF N = 0 THEN 7740

7580 P = 1 / N

7590 M = 1

7600 FOR I = 1 TO N

7610 PRINT " ITEM"; I;

7620 INPUT " = ", D

7630 M = M * D ^ P

7640 Q = Q + LOG(D) ^ 2

7650 NEXT I

7660 R = EXP(SQR(Q / (N - 1) - (N / (N - 1) * (LOG(M)) ^ 2)))

7670 PRINT : COLOR 11

7680 PRINT USING " GEOMETRIC MEAN      = ##.###^^^^ "; M

7690 PRINT

7700 PRINT USING " GEOMETRIC DEVIATION = ##.###^^^^ "; R

7710 PRINT : COLOR 14

7720 PRINT "****************************************************************"

7730 PRINT : GOTO 7550

7740 GOTO 100



8000 REM     INTEGRAL.BAS         DECEMBER 26, 1992          J. KANTELE

8010 COLOR 14, 5: CLS

8020 PRINT

8030 PRINT " LIST OF SOME COMMON INTEGRAL FUNCTIONS:  "

8040 PRINT

8050 PRINT "  FUNCTION              INTEGRAL "

8060 PRINT

8070 PRINT "  x^n ; (n<> -1)         x^(n + 1)/(n + 1) "

8080 PRINT "  (ax + b)^n; (n<> -1)  (ax + b)^(n + 1) / ((n + 1)a) "

8090 PRINT "  1/x ; 1/(ax + b)      ln |x| ; ln (ax + b) /a"

8100 PRINT "  f'(x)/f(x)            ln f(x) "

8110 PRINT "  f'(x)f(x)             ´[f(x)]^2"

8120 PRINT "  e^x ; e^(ax) ; a^x    e^x ; 1/a e^(ax) ; a^x/ln a "

8130 PRINT "  xe^(ax)               e^(ax)(ax - 1)/a^2"

8140 PRINT "  1/(x - a)             ln |x-a| "

8150 PRINT "  1/(x - a)(x - b)      1/(a - b) ln|(x - a)/(x - b)| "

8160 PRINT "  1/(x - a)^2           -1/(x - a) "

8170 PRINT "  1/(x^2 + a^2)         1/a atan (x/a) "

8180 PRINT "  x/(x^2 + a^2)         1/2 ln (x^2 + a^2) "

8190 PRINT "  x SQR(ax + b)       [2/(5a^2)][SQR(ax + b)]^5 - [2b/(3a^2)][SQR(ax + b)]^3 "

8200 PRINT "  SQR(x^2 + a^2)      (x/2) SQR(x^2 + a^2) + (a^2/2) ln (x + SQR(x^2 + a^2)) "

8210 PRINT "  SQR(x^2 - a^2)      (x/2) SQR(x^2 - a^2) - (a^2/2) ln |x + SQR(x^2 - a^2)| "

8220 PRINT "  SQR(a^2 - x^2)      (x/2) SQR(a^2 - x^2) + (a^2/2) arc sin (x/a) "

8230 PRINT : COLOR 15

8240 PRINT " HIT A KEY TO CONTINUE WITH THE LIST "

8250 IF INKEY$ = "" THEN 8250

8260 CLS : PRINT

8270 PRINT : COLOR 14

8280 PRINT "  ln x                  x ln x - x "

8290 PRINT "  x ln x                (x^2/2) ln x - x^2 /4"

8300 PRINT "  x^n ln (ax)           [x^(n+1)/(n+1)] ln (ax) - [x^(n+1)]/(n+1)^2 "

8310 PRINT "  1/SQR(a^2 - x^2)      arc sin(x/a) "

8320 PRINT "  1/SQR(x^2 + a^2)      ln (x + SQR(x^2 + a^2) "

8330 PRINT "  1/SQR(x^2 - a^2)      ln |x + SQR(x^2 - a^2)| "

8340 PRINT "  x/(x^2 + a^2)^(3/2)   (x/a^2) /SQR(x^2 + a^2) "

8350 PRINT

8360 PRINT "  sin ax                -1/a cos ax"

8370 PRINT "  sin^2 ax              1/(2a)(ax - sin ax cos ax)"

8380 PRINT "  1/sin ax              1/a ln |tan (ax/2)| "

8390 PRINT "  1/sin^2 ax            -1/a cot ax "

8400 PRINT "  cos ax                1/a sin ax"

8410 PRINT "  cos^2 ax              1/(2a)(ax + sin ax cos ax)"

8420 PRINT "  1/cos ax              1/a ln |tan (Pi/4 + ax/2)|"

8430 PRINT "  1/cos^2 ax            1/a tan ax "

8440 PRINT "  x sin ax              (sin ax)/a^2 - (x cos ax)/a "

8450 PRINT "  x^2 sin ax            (2x/a^2) sin ax + (2/a^3 - x^2/a) cos ax "

8460 PRINT "  x cos ax              (cos ax)/a^2 + (x sin ax)/a"

8470 PRINT "  x^2 cos ax            (2x/a^a) cos ax + (x^2/a - 2/a^3) sin ax "

8480 PRINT : COLOR 15

8490 PRINT " HIT A KEY TO RETURN TO THE MENU "

8500 IF INKEY$ = "" THEN 8500

8510 GOTO 100



9000 COLOR 14, 5: CLS : PRINT

9010 PRINT " NUMERICAL INTEGRATION: GAUSSIAN QUADRATURE"

9020 PRINT

9030 PRINT " DEFINE THE FUNCTION TO BE INTEGRATED IN LINE 9040 [DEF FNC (X)]. "

9040 DEF FNC (X) = 1 / X ^ 2

9050 DATA .076526521,.15275339,.22778585,.14917299,.37370609

9060 DATA .14209611,.510867,.13168864,.63605368,.11819453

9070 DATA .74633191,.10193012,.83911697,.083276742,.91223443

9080 DATA .062672048,.96397193,.04060143,.9931286,.017614007

9090 PRINT

9100 INPUT " INTEGRATION LIMITS (LOWER, UPPER), X1, X2 = ", X1, X2

9110 PRINT

9120 INPUT " NUMBER OF INTERVALS, N = ", N

9130 S = (X2 - X1) / N / 2

9140 T = X1 + S

9150 R = 0

9160 FOR I = 1 TO N

9170 P = 0

9180 FOR J = 1 TO 10

9190 READ A, B

9200 P = P + B * (FNC(S * A + T) + FNC(T - S * A))

9210 NEXT J

9220 RESTORE

9230 R = R + P * S

9240 T = T + 2 * S

9250 NEXT I

9260 PRINT : COLOR 11

9270 PRINT USING " INTEGRAL = ##.####^^^^ "; R

9280 PRINT : COLOR 14

9290 PRINT " CHANGE DATA AND RECOMPUTE?"

9300 PRINT

9310 PRINT " (0 = NO, 1 = NEW INTEGRATION LIMITS, 2 = NEW NO. OF INTERVALS)";

9320 INPUT S

9330 IF S = 1 THEN 9090

9340 IF S = 2 THEN 9110

9350 GOTO 100



9500 REM   PROBDIST.BAS            JANUARY 18, 1993         J. KANTELE

9510 COLOR 14, 4: CLS

9520 PRINT

9530 PRINT " PROGRAM TO CALCULATE THE PROBABILITY THAT A MEASURED VALUE FALLS "

9540 PRINT " WITHIN A CERTAIN INTERVAL FROM THE EXACT AVERAGE VALUE TAKEN AS = 0."

9550 PRINT " GAUSSIAN DISTRIBUTION IS ASSUMED, WITH STANDARD VEVIATION S.D. = 1. "

9560 PRINT " THE INTEGRATION METHOD IS THE GAUSSIAN QUADRATURE. "

9570 REM

9580 DEF FND (X) = .398942281# * EXP(-X ^ 2 / 2)

9590 DATA .076526521,.15275339,.22778585,.14917299,.37370609

9600 DATA .14209611,.510867,.13168864,.63605368,.11819453

9610 DATA .74633191,.10193012,.83911697,.083276742,.91223443

9620 DATA .062672048,.96397193,.04060143,.9931286,.017614007

9630 PRINT

9640 INPUT " ENTER THE UPPER (INTEGRATION) LIMIT, XU = ", X2

9650 PRINT

9660 INPUT " ENTER THE LOWER (INTEGRATION) LIMIT, XL = ", X1

9670 PRINT

9680 PRINT " NUMBER OF INTERVALS";

9690 INPUT " = ", N

9700 S = (X2 - X1) / N / 2

9710 T = X1 + S

9720 R = 0

9730 FOR I = 1 TO N

9740 P = 0

9750 FOR J = 1 TO 10

9760 READ A, B

9770 P = P + B * (FND(S * A + T) + FND(T - S * A))

9780 NEXT J

9790 RESTORE

9800 R = R + P * S

9810 T = T + 2 * S

9820 NEXT I

9830 PRINT : COLOR 11

9840 PRINT USING " PROBABILITY P = ##.######^^^^ %"; R * 200

9850 PRINT : COLOR 14

9860 PRINT " CHANGE DATA AND RECOMPUTE?"

9870 PRINT

9880 PRINT " (0 = NO, 1 = NEW INTEGRATION LIMITS, 2 = NEW NO. OF INTERVALS)";

9890 INPUT U

9900 IF U = 1 THEN 9630

9910 IF U = 2 THEN 9670

9920 GOTO 100

9930 END



