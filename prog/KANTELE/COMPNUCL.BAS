100 REM    COMPNUCL.BAS        MARCH 6, 1991        J. KANTELE

110 COLOR 14, 5: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO COMPUTE THE VELOCITY, AND KINETIC AND EXCITATION "

150 PRINT " ENERGY OF A COMPOUND NUCLEUS. THE CALCULATION IS RELATIVISTIC."

160 PRINT " IF A C.N. IS NOT FORMED, THE LAB. SPEED AND KINETIC ENERGY REFER "

170 PRINT " TO THE INTERMEDIATE SYSTEM REGARDLESS OF ITS CHARACTER."

180 PRINT

190 PRINT "*****************************************************************"

200 PRINT

210 C = 29.97925: U = 931.4934

220 INPUT " MASS OF THE PROJECTILE (IN u), M1 = ", M1

230 PRINT

240 INPUT " MASS OF THE TARGET ATOM (IN u), M2 = ", M2

250 PRINT

260 PRINT " NEXT YOU GIVE THE FUSION Q-VALUE (CHOICE = 1) OR THE MASS OF THE "

270 INPUT " COMPOUND NUCLEUS (CHOICE = 2). YOUR CHOICE (1 OR 2) = ", N: PRINT

280 ON N GOTO 290, 300

290 INPUT " Q(MeV) = ", Q: MC = M1 + M2 - Q / U: GOTO 320

300 INPUT " MASS OF COMPOUND NUCLEUS (IN GROUND STATE; IN u) M(C.N.) = ", MC

310 Q = (M1 + M2 - MC) * U

320 COLOR 14: PRINT

330 INPUT " ENERGY (LAB.) OF THE PROJECTILE (IN MeV), E1 = ", E

340 PRINT : REM V1 = SPEED OF PROJECTILE

350 V1 = SQR(E ^ 2 + 2 * E * M1 * U) / (E + M1 * U) * C

360 B1 = V1 / C

370 REM VCM = SPEED OF CENTER-OF-MASS; EX = EXCITATION ENERGY OF THE C.N.

380 VCM = M1 * V1 / (M1 + M2 * SQR(1 - B1 ^ 2)): VCN = VCM

390 EX = E * M2 / (M1 + M2) + Q

400 EC = E * (1 - M2 / (M1 + M2)) / (M1 + M2 - Q / U)

410 IF EX > 0 THEN 440

420 COLOR 10

430 PRINT " ** FUSION IS NOT POSSIBLE AT THIS BOMBARDING ENERGY **": GOTO 320

440 PRINT

450 PRINT USING " SPEED OF THE COMPOUND NUCLEUS V(C.N.) = ##.#### cm/ns"; VCN

460 PRINT

470 PRINT USING " KINETIC ENERGY OF THE C.N., E(kin) = ##.###^^^^ MeV/u"; EC

480 PRINT

490 PRINT USING " EXCITATION ENERGY OF THE C.N., E(ex) = ##.###^^^^ MeV"; EX

500 PRINT

510 PRINT "*****************************************************************"

520 PRINT

530 PRINT " HIT A KEY WHEN READY"

540 IF INKEY$ = "" THEN 540 ELSE 550

550 COLOR 15, 1: CLS

560 LOCATE 3, 10: PRINT "YOUR CHOICES FOR CONTINUATION ARE:"

570 LOCATE 7, 10: PRINT "1. CHANGE INPUT Q AND E, KEEPING PREVIOUS M1 AND M2"

580 LOCATE 9, 10: PRINT "2. CHANGE E ONLY, KEEPING M1, M2, AND Q UNCHANGED"

590 LOCATE 11, 10: PRINT "3. RESTART THE WHOLE PROGRAM"

600 LOCATE 13, 10: PRINT "4. QUIT"

610 LOCATE 17, 10: PRINT "ENTER YOUR CHOICE BY NUMBER (1 TO 4)"

620 V$ = INKEY$: IF VAL(V$) < 1 OR VAL(V$) > 4 THEN 600

630 V = VAL(V$): COLOR 14, 5: CLS : PRINT

640 ON V GOTO 250, 320, 180, 650

650 END

