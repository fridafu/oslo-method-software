100 REM    NEGASHAP.BAS          SEPTEMBER 9, 1992       J. KANTELE

110 COLOR 15, 1: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE THE SHAPES OF ALLOWED BETA-RAY (-) ENERGY "

150 PRINT " SPECTRA, BASED ON BEHRENS AND JéNECKE'S TABLES [Be69]."

160 PRINT " THE PROGRAM IS VALID FOR Z = 1 TO 100, AND E = 2 keV TO 20 MeV."

170 PRINT " THE SREEENING IS NOT CORRECTED FOR; IT CAN USUALLY BE IGNORED. "

180 PRINT " THE SPECTRUM NORMALIZATION: TOTAL NUMBER OF COUNTS = 10,000;"

190 PRINT " IT CAN BE ADJUSTED BY CHANGING THE FACTOR 'NR' ON LINE 230."

200 PRINT " THE ENERGY-BIN (CHANNEL) NUMBER CAN BE CHOSEN (1 TO 200)."

210 PRINT

220 PRINT "*****************************************************************"

230 NR = 10000

240 DIM M(8, 11), E(200), F(200), ME(200), NE(200), P(200)

250 FOR K = 1 TO 8

260     FOR L = 1 TO 11

270         READ M(K, L)

280     NEXT L

290 NEXT K

300 DATA 1, .3269, -.12948, .019016, -.00095348, 0, .0272646, -.0004201, -9.5474E-6, 0, 0

310 DATA 10, 2.08738, -.57928, .0404785, .00305364, -.000334, .11416, .043251, -.0033661, 0, 0

320 DATA 20, 2.80848, -.404105, -.068659, .019067, -.0010833, .127189, .128762, -.010038, 0, 0

330 DATA 30, 3.33244, -.300744, -.11193, .023452, -.0012114, .083368, .244539, -.019246, 0, 0

340 DATA 50, 4.38334, -.319865, -.091311, .016894, -.0007709, .047726, .515291, -.0424, 0, 0

350 DATA 70, 5.51184, -.38239, -.058665, .0107613, -.000491881, .515219, .738253, -.066362, 0, 0

360 DATA 90, 6.76374, -.41807, -.042962, .0086018, -.00051951, 2.52482, .66674, -.076984, 0, 0

370 DATA 100, 7.4614, -.437171, -.035496, .0077881, -.00057477, 4.4908, .42503, -.071491, 0, 0

380 REM

390 PRINT

400 INPUT " ATOMIC NUMBER OF DAUGHTER NUCLEUS, Z = ", Z

410 PRINT

420 FOR I = 1 TO 7

430 IF M(I + 1, 1) - Z >= 0 THEN 450

440 NEXT I

450 A = M(I + 1, 1): B = M(I, 1)

460 INPUT " END-POINT ENERGY OF BETA SPECTRUM (keV), E(max) = ", E0

470 PRINT

480 INPUT " NUMBER OF ENERGY BINS (CHANNELS), MAX. 200, NB = ", NB

490 PRINT : NE = 0

500 INPUT " DO YOU WANT PRINTED RESULTS (1 = YES; 2 = NO)? CHOICE = ", PR

510 PRINT :

520 FOR J = 1 TO NB

530 E(J) = J * E0 / NB - E0 / (2 * NB)

540 IF E(J) > 2000 THEN G = 5 ELSE G = 0

550 FOR K = I TO I + 1

560 GOSUB 870

570 NEXT K

580 F(J) = EXP(LOG(Y(I)) + (LOG(Y(I + 1)) - LOG(Y(I))) * (Z - M(I, 1)) / (A - B))

590 P(J) = SQR(E(J) ^ 2 + 1022 * E(J)) * 100

600 NE(J) = (E0 - E(J)) ^ 2 * (E(J) + 511) * F(J) * P(J)

610 NE = NE + NE(J)

620 NEXT J

630 COLOR 14: PRINT USING " Z = ##; E(max) = ##.###^^^^ keV "; Z; E0

640 PRINT

650 IF PR = 2 THEN 710

660 LPRINT

670 LPRINT USING " Z = ##; E(max) = ##.###^^^^ keV "; Z; E0

680 LPRINT

690 LPRINT " CHANNEL ENERGY       COUNTS/CHANNEL "

700 LPRINT

710 PRINT " CHANNEL ENERGY       COUNTS/CHANNEL "

720 PRINT

730 FOR J = 1 TO NB

740 ME(J) = NE(J) * NR / NE

750 IF PR = 1 THEN LPRINT USING " E = ##.###^^^^ keV ; N(E) = ####.## "; E(J); ME(J)

760 COLOR 14: PRINT USING " E = ##.###^^^^ keV ; N(E) = ####.## "; E(J); ME(J)

770 NEXT J

780 PRINT : COLOR 15

790 PRINT " HOW TO CONTINUE? THE CHOICES: 1 = CALCULATE SHAPE FOR THE SAME Z;"

800 PRINT

810 INPUT " 2 = FOR ANOTHER Z; 3 = RESTART PROGRAM; 4 = QUIT. YOUR CHOICE = ", M

820 PRINT

830 PRINT "*****************************************************************"

840 PRINT

850 ON M GOTO 460, 400, 140, 860

860 END

870 Y(K) = EXP(M(K, 2 + G) + M(K, 3 + G) * LOG(E(J)) + M(K, 4 + G) * LOG(E(J)) ^ 2 + M(K, 5 + G) * LOG(E(J)) ^ 3 + M(K, 6 + G) * LOG(E(J)) ^ 4)

880 RESTORE

890 RETURN



