100 REM    LIONTRAN.BAS          MARCH 17, 1990         J. KANTELE

110 COLOR 14: CLS

120 PRINT "******************************************************************"

130 PRINT

140 PRINT "PROGRAM TO CALCULATE THE DEPOSITED AND RESIDUAL ENERGIES OF H"

150 PRINT "AND HE IONS PASSING A SILICON DETECTOR WITH A GIVEN THICKNESS."

160 PRINT

170 PRINT "THE CALCULATIONS ARE BASED ON A PROTON-RANGE FORMULA GOOD TO A"

180 PRINT "FEW PER CENT IN THE ENERGY RANGE FROM 1 MeV TO 50 MeV."

190 PRINT

200 PRINT "******************************************************************"

210 PRINT

220 INPUT "PROJECTILE # (1 = p, 2 = d, 3 = t, 4 = 3-He, 5 = 4-He) = ", N

230 PRINT

240 IF N > 5 THEN 250 ELSE 260

250 PRINT "THE PROJECTILE NUMBER MUST BE < 6!": GOTO 220

260 INPUT "PROJECTILE ENERGY (MeV) = ", G

270 PRINT

280 INPUT "THICKNESS 0F SILICON DETECTOR (micrometers) = ", D

290 PRINT

300 IF N = 1 THEN 310 ELSE 320

310 P = G: GOTO 390

320 IF N = 2 THEN 330 ELSE 340

330 P = .5 * G: GOTO 390

340 IF N = 3 THEN 350 ELSE 360

350 P = .333 * G: GOTO 390

360 IF N = 4 THEN 370 ELSE 380

370 P = .28 * G: GOTO 390

380 P = .25 * G

390 PRINT

400 REM  P IS THE ENERGY PER NUCLEON (MeV/u)

410 LET Q = 1 + (N - 1) * (N - 4) * (N - 5) / 6 + (N - 1) * (N - 2) * (N - 4) * (N - 5) / 3

420 LET R = Q * 4.23 * EXP(1.361 + 1.541 * LOG(P) + .04004 * (LOG(P)) ^ 2)

430 LET X = R - D

440 IF X < 0 THEN 450 ELSE 470

450 PRINT "THE DETECTOR STOPS THE PARTICLE COMPLETELY": GOTO 190

460 PRINT

470 LET E = EXP(SQR(300.18 + LOG((R - D) / Q) / .04004) - 19.24) / P * G

480 LET C = G - E

490 PRINT USING "THE ENERGY DEPOSITED IN THE DETECTOR IS = ##.##^^^^ MeV"; C

500 PRINT

510 PRINT USING "THE ENERGY OF THE TRANSMITTED PARTICLE IS = ##.##^^^^ MeV"; E

520 GOTO 190

530 END



