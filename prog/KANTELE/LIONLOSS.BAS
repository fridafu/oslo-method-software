100 REM    LIONLOSS.BAS          MAY 10, 1993         J. KANTELE

110 COLOR 14, 4: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE THE MEAN ENERGY LOSS OF LIGHT IONS IN A "

150 PRINT " LAYER OF THICKNESS D OF A SOLID ELEMENT WITH  Z > 5, AND THE "

160 PRINT " MEAN RESIDUAL ENERGY. THE ENERGY RANGE IS .3 TO 300 MeV/u, AND "

170 PRINT " THE ION ENERGIES ARE ENTERED IN MeV. "

180 PRINT

190 PRINT " THE CALCULATIONS ARE BASED ON FITS ON PROTON DATA POINTS FOR "

200 PRINT " Z = 6, 13, 26, AND 82, FROM: J. F. JANNI, ADNDT 27, (1982) 341."

210 PRINT

220 PRINT " THE PROGRAM USES THE DATA AND SOME OF THE TECHNIQUES OF THE "

230 PRINT " CODE 'LIONSTOP.BAS'. FOR THIN ABSORBER/TARGET LAYERS (A FEW "

240 PRINT " mg/cm^2), THE ACCURACY IS FAIRLY GOOD. FOR VERY LARGE "

250 PRINT " PENETRATION THICKNESSES, THE RESULTS ARE APPROXIMATE. "

260 PRINT

270 PRINT "*****************************************************************"

280 REM

290 DIM T(10, 11)

300 FOR K = 1 TO 10

310     FOR L = 1 TO 11

320         READ T(K, L)

330     NEXT L

340 NEXT K

350 DATA .1, .3, .7, 1, 3, 7, 10, 30, 70, 100, 300

360 DATA 1, 4.4, 14, 24, 160, 680, 1300, 9000, 4E+4, 7.5E+4, 5.2E+5

370 DATA 6, .49, 1.6, 2.8, 16.7, 72, 136, 979, 4514, 8538, 5.7E+4

380 DATA 13, .79, 2.4, 3.9, 21.9, 91, 168, 1170, 5270, 9900, 6.5E+4

390 DATA 26, 1.09, 3.1, 5.1, 28, 111, 203, 1360, 6010, 1.12E+4, 7.26E+4

400 DATA 82, 2.2, 6.3, 10.3, 52, 192, 339, 2080, 8780, 1.6E+4, 1E+5

410 DATA 6, 505, 288, 229, 106, 55, 41, 16.9, 8.6, 6.6, 3.2

420 DATA 13, 311, 213, 172, 83, 45, 34, 14.4, 7.5, 5.7, 2.8

430 DATA 26, 257, 159, 131, 66.8, 37, 28.7, 12.5, 6.6, 5.08, 2.51

440 DATA 82, 109, 76, 64, 37, 21, 17.7, 8.4, 4.6, 3.6, 1.8

450 PRINT : NR = 0: D1 = 0

460 INPUT " PROJECTILE # (1 = p, 2 = d, 3 = t, 4 = 3-He, 5 = 4-He) = ", N

470 PRINT

480 IF N > 5 THEN 490 ELSE 510

490 PRINT " PROJECTILE NUMBER MUST BE 1, 2, 3, 4, or 5 !": GOTO 450

500 PRINT

510 INPUT " PROJECTILE ENERGY (MeV); [THE RANGE IS .3 TO 300 MeV/u], E = ", G

520 PRINT : G1 = G: NR = 0: IF NC = 2 THEN 580

530 INPUT " ATOMIC NUMBER OF ABSORBER (Z > 5), Z = ", Z

540 PRINT : D1 = 0: G = G1

550 INPUT " ABSORBER THICKNESS (mg/cm^2), D = ", D1

560 PRINT : NR = 0

570 IF Z < 6 THEN 530

580 IF NR = 0 THEN 660

590 IF D1 >= R1 THEN G = 0: GOTO 1130

600 FOR X = 1 TO 100

610 IF X * R1 / 100 >= D1 THEN 630

620 NEXT X

630 FOR Y = 1 TO X

640 G = G - S1 * R1 / 100 / 1000

650 IF G < .1 THEN G = .05

660 IF N = 1 THEN 670 ELSE 680

670 P = G: GOTO 750

680 IF N = 2 THEN 690 ELSE 700

690 P = .5 * G: GOTO 750

700 IF N = 3 THEN 710 ELSE 720

710 P = .333 * G: GOTO 750

720 IF N = 4 THEN 730 ELSE 740

730 P = .28 * G: GOTO 750

740 P = .25 * G: REM P IS THE ENERGY PER NUCLEON (MeV/u)

750 FOR J = 1 TO 10

760 IF T(1, J + 1) - P >= 0 THEN 780

770 NEXT J

780 IF NR = 1 THEN 930

790 FOR I = 2 TO 6

800 GOSUB 1330

810 NEXT I

820 IF Z > 82 THEN 870

830 FOR M = 2 TO 5

840 IF T(M + 1, 1) - Z >= 0 THEN 860

850 NEXT M

860 LET R = Y(M + 1) + (Y(M) - Y(M + 1)) * ((T(M + 1, 1) - Z) / (T(M + 1, 1) - T(M, 1))): GOTO 880

870 LET R = Y(6) + (Y(6) - Y(5)) * (Z - 82) / (82 - 26): GOTO 880

880 LET Q = 1 + (N - 1) * (N - 4) * (N - 5) / 6 + (N - 1) * (N - 2) * (N - 4) * (N - 5) / 3

890 LET R1 = Q * R

900 LET R2 = (Y(4) + (Y(5) - Y(4)) / 13) * 4.31 * Q

910 LET R3 = (Y(5) + .107 * (Y(6) - Y(5))) * 1.88 * Q

920 LET R4 = Y(2) * Q

930 FOR I = 7 TO 10

940 GOSUB 1330

950 NEXT I

960 IF Z > 82 THEN 1010

970 FOR U = 6 TO 9

980 IF T(U + 1, 1) - Z >= 0 THEN 1000

990 NEXT U

1000 S = Y(U + 1) + (Y(U) - Y(U + 1)) * ((T(U + 1, 1) - Z) / (T(U + 1, 1) - T(U, 1))) ^ 2: GOTO 1020

1010 S = Y(10) + (Y(10) - Y(9)) * ((Z - 82) / (82 - 26)) / 2: GOTO 1020

1020 IF N < 4 THEN V = 1

1030 IF N = 4 THEN V = 3.6

1040 IF N = 5 THEN V = 4

1050 S1 = V * S

1060 IF NR = 0 THEN 1090

1070 NEXT Y

1080 GOTO 1100

1090 NR = 1: GOTO 580

1100 IF X * R1 / 100 > D1 THEN G = G + S1 / 1000 * (X * R1 / 100 - D1)

1110 IF G < .1 THEN G = 0

1120 COLOR 15

1130 PRINT USING " FULL RANGE IN ELEMENT Z = ## IS = ##.##^^^^ mg/cm^2"; Z; R1

1140 PRINT

1150 PRINT USING " ENERGY LOST IN D = ##.###^^^^ mg/cm^2  IS  E(dep) = ##.###^^^^ MeV"; D1; G1 - G

1160 PRINT

1170 PRINT USING " RESIDUAL ENERGY  E(final) = ##.###^^^^ MeV "; G

1180 PRINT : COLOR 14

1190 PRINT "****************************************************************"

1200 PRINT

1210 COLOR 15: PRINT " TO CONTINUE, YOU HAVE THE FOLLOWING CHOICES: "

1220 PRINT

1230 PRINT " 1. RESTART THE PROGRAM "

1240 PRINT " 2. CHANGE THE INPUT ENERGY "

1250 PRINT " 3. CHANGE THE ABSORBER THICKNESS "

1260 PRINT " 4. QUIT "

1270 PRINT

1280 INPUT " MAKE YOUR CHOICE BY NUMBER (1...4). CHOICE = ", NC

1290 PRINT : COLOR 14

1300 PRINT "***************************************************************"

1310 ON NC GOTO 450, 500, 540, 1320

1320 END

1330 Y(I) = EXP(LOG(T(I, J)) + (LOG(P) - LOG(T(1, J))) / (LOG(T(1, J + 1)) - LOG(T(1, J))) * (LOG(T(I, J + 1)) - LOG(T(I, J))))

1340 RETURN

