100 REM   SCREENEG.BAS         AUGUST 11, 1992          J. KANTELE

110 COLOR 15, 1: CLS

120 PRINT "****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE THE SCREENING CORRECTIONS [= RATIO OF "

150 PRINT " SCREENED AND UNSCREENED FERMI (OR COULOMB) FUNCTIONS] IN "

160 PRINT " NEGATRON DECAY. THE PROGRAM IS BASED ON FITTING CURVES TO"

170 PRINT " POINTS TABULATED BY BEHRENS AND JÈNECKE [Be69]."

180 PRINT " THE PROGRAM ALLOWS EXTRAPOLATION ABOVE THE ENERGY RANGE "

190 PRINT " OF THE TABULATED POINTS. "

200 PRINT " THE RANGE OF VALIDITY IS 1 < Z < 105; E > 2 keV."

210 PRINT " THE CALCULATED CORRECTION FACTORS AGREE EVERYWHERE WITH THE "

220 PRINT " ORIGINAL ONES AT LEAST TO WITHIN 1 PER CENT."

230 PRINT

240 PRINT "*****************************************************************"

250 PRINT

260 INPUT " ATOMIC NUMBER OF DAUGHTER NUCLEUS, Z = ", Z

270 PRINT

280 INPUT " KINETIC ENERGY OF ELECTRON (keV), E = ", E

290 PRINT

300 FOR K = 1 TO 5

310   FOR L = 1 TO 5

320      READ T(K, L)

330   NEXT L

340 NEXT K

350 DATA 10, -5.7046, 1.28, -0.38514, 0.024952

360 DATA 30, -4.5982, -.1851, .1766, -.02585

370 DATA 50, -3.529, -.2721, .11183, -.01394

380 DATA 70, -3.1844, -.2043, .08232, -.01106

390 DATA 90, -2.9835, -.17543, .07325, -.01046

400 IF Z > 90 THEN I = 4: GOTO 440

410 FOR I = 1 TO 4

420 IF T(I + 1, 1) - Z >= 0 THEN 440

430 NEXT I

440 FOR K = I TO I + 1

450 GOSUB 510

460 NEXT K

470 C = 1 - EXP(Y(I) + (Y(I + 1) - Y(I)) * (Z - T(I, 1)) / (T(I + 1, 1) - T(I, 1)))

480 PRINT USING " SCREENING CORRECTION FACTOR = ##.###^^^^ "; C

490 GOTO 240

500 END

510 Y(K) = T(K, 2) + T(K, 3) * LOG(E) + T(K, 4) * LOG(E) ^ 2 + T(K, 5) * LOG(E) ^ 3

520 RESTORE

530 RETURN

