100 REM    LISTOP.BAS          MARCH 17, 1990         J. KANTELE

110 COLOR 12: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT "SIMPLE PROGRAM TO CALCULATE RANGES AND STOPPING POWERS OF H AND"

150 PRINT "HE IONS (ABOUT 1 TO 100 MEV/u)"

160 PRINT

170 PRINT "THE CALCULATIONS ARE BASED ON FITS TO PROTON DATA POINTS FROM:"

180 PRINT "J. F. JANNI, ADNDT 27,341 (1982)."

190 PRINT

200 PRINT "THE RANGE ACCURACY IS GENERALLY ABOUT 10 %, EXCEPT FOR VALUES"

210 PRINT "INVOLVING LARGE INTERPOLATION STEPS; THE STOPPING POWER VALUES"

220 PRINT "ARE ONLY APPROXIMATE."

230 PRINT

240 PRINT "*******************************************************************"

250 PRINT

260 INPUT "PROJECTILE # (1 = p,2 = d,3 = t,4 = 3-He,5 = 4-He) = ", N

270 PRINT

280 INPUT "PROJECTILE ENERGY (MeV) = ", E

290 PRINT

300 INPUT "ABSORBER Z = ", Z

310 G = E: GOTO 330

320 G = .99 * E

330 IF N = 1 THEN 340 ELSE 350

340 P = G: GOTO 420

350 IF N = 2 THEN 360 ELSE 370

360 P = .5 * G: GOTO 420

370 IF N = 3 THEN 380 ELSE 390

380 P = .333 * G: GOTO 420

390 IF N = 4 THEN 400 ELSE 410

400 P = .28 * G: GOTO 420

410 P = .25 * G

420 PRINT

430 R6 = EXP(1.62116 + 1.53197 * LOG(P) + .031256 * (LOG(P)) ^ 2)

440 R8 = EXP(2.32834 + 1.44228 * LOG(P) + .034068 * (LOG(P)) ^ 2)

450 R = R6 + .01786 * (Z - 26) * (R8 - R6)

460 LET Q = 1 + (N - 1) * (N - 4) * (N - 5) / 6 + (N - 1) * (N - 2) * (N - 4) * (N - 5) / 3

470 IF G = E THEN X = Q * R

480 IF G = E THEN 320 ELSE 490

490 H = Q * R

500 F = 10 * E / (X - H)

510 LET R1 = Q * R

520 R2 = (R6 - .214 * (R8 - R6)) * 4.29 * Q

530 R3 = (R6 + .1072 * (R8 - R6)) * 1.88 * Q

540 PRINT USING "RANGE IN SILICON = ##.##^^^^ micrometers"; R2

550 PRINT

560 PRINT USING "RANGE IN GERMANIUM = ##.##^^^^ micrometers"; R3

570 PRINT

580 PRINT USING "RANGE IN ELEMENT Z = ## IS = ##.##^^^^ mg/cm^2"; Z; X

590 PRINT

600 PRINT USING "APPROX. STOPPING POWER dE/dx = ##.##^^^^ keV/mg/cm^2"; F

610 PRINT

620 COLOR 11: PRINT "TO QUIT, JUST HIT CTRL-C ": COLOR 12

630 GOTO 240

640 END

