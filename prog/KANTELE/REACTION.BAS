100 REM    REACTION.BAS         MARCH 3, 1991        J. KANTELE

110 COLOR 15, 1: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT "PROGRAM TO CALCULATE KINEMATIC QUANTITIES OF SIMPLE NUCLEAR "

150 PRINT "REACTIONS (WITH TWO PARTICLES IN INPUT AND OUTPUT CHANNELS). "

160 PRINT "NON-RELATIVISTIC TREATMENT."

170 PRINT

180 PRINT "HERE ARE SOME LIGHT ATOMIC MASSES (IN u): n = 1.008 6649; H-1 = "

190 PRINT "1.007 8250; H-2 = 2.014 1018; H-3 = 3.016 049; He-3 = 3.016 029;"

200 PRINT "He-4 = 4.002 603; 0-16 = 15.994 915; Ne-20 = 19.992 436."

210 PRINT

220 PRINT "*****************************************************************"

230 DEFDBL A-Z

240 PRINT

250 PRINT "ENTER THE FOLLOWING QUANTITIES:"

260 PRINT

270 INPUT "MASSES M1, M2, M3, AND M4 (a.m.u.) = ", M1, M2, M3, M4

280 PRINT

290 IF M1 + M2 > (M3 + M4) * 1.01 THEN GOTO 310 ELSE 300

300 IF (M1 + M2) * 1.01 < M3 + M4 THEN GOTO 310 ELSE 330

310 PRINT "YOU MUST HAVE APPROXIMATELY  M1 + M2 = M3 + M4 !!"

320 PRINT : GOTO 270

330 INPUT "Q-VALUE (MeV) = ", Q

340 PRINT

350 INPUT "INCIDENT (LAB.) ENERGY E1 (MeV) = ", E1

360 PRINT

370 INPUT "LAB. ANGLE OF LIGHT PRODUCT M3,PSI (DEGR.) = ", PSI

380 LET E5 = E1 + Q

390 LET A = M1 * M4 * (E1 / E5) / (M1 + M2) / (M3 + M4)

400 LET B = M1 * M3 * (E1 / E5) / (M1 + M2) / (M3 + M4)

410 LET C = M2 * M3 / (M1 + M2) / (M3 + M4) * (1 + M1 * Q / M2 / E5)

420 LET D = M2 * M4 / (M1 + M2) / (M3 + M4) * (1 + M1 * Q / M2 / E5)

430 IF B > D THEN GOTO 440 ELSE GOTO 530

440 PRINT

450 LET Z = SQR(D / B)

460 LET Y = ATN(Z / SQR(-Z * Z + 1))

470 LET Y1 = Y * 57.29578

480 IF PSI >= Y * 57.29578 THEN GOTO 490 ELSE GOTO 510

490 PRINT USING "PSI MUST BE SMALLER THAN ###.## DEGR."; Y1

500 GOTO 360

510 LET E3 = E5 * B * (COS((PSI) / 57.29578) - (D / B - (SIN((PSI) / 57.29578)) ^ 2) ^ (.5)) ^ 2

520 GOTO 540

530 LET E3 = E5 * B * (COS((PSI) / 57.29578) + (D / B - (SIN((PSI) / 57.29578)) ^ 2) ^ (.5)) ^ 2

540 LET E4 = E5 - E3

550 LET X = (M3 * E3 / M4 / E4) ^ (.5) * SIN((PSI) / 57.29578)

560 LET TSETA = ATN(X / SQR(-X * X + 1)) * 57.29578

570 PRINT : PRINT

580 PRINT USING "THE LAB. ENERGY OF THE LIGHT PRODUCT IS: E3 = ###.#### MeV"; E3

590 PRINT

600 PRINT USING "THE LAB. ANGLE OF THE HEAVY PRODUCT IS: TSETA = ###.## DEGR."; TSETA

610 PRINT

620 PRINT USING "THE LAB. ENERGY OF THE HEAVY PRODUCT IS: E4 = ###.#### MeV"; E4

630 PRINT

640 PRINT "*****************************************************************"

650 COLOR 12: PRINT "IF YOU LIKE TO QUIT, JUST HIT CTRL-C ": COLOR 15: GOTO 170

660 END

