100 REM    RECOIL.BAS         NOVEMBER 27, 1990         J. KANTELE

110 COLOR 14, 5: CLS

120 PRINT "*****************************************************************"

130 PRINT

140 PRINT " PROGRAM TO CALCULATE RECOIL ENERGY OF THE HEAVY PARTICLE (ATOM,"

150 PRINT " NUCLEUS, HEAVY FISSION FRAGMENT) IN PHOTON, ELECTRON, ALPHA,"

160 PRINT " AND SPONTANEOUS-FISSION (SF) DECAY OF ATOM/NUCLEUS IN REST. THE "

170 PRINT " TRANSITION ENERGY IN THE EMISSION OF A PHOTON, MONOENERGETIC "

180 PRINT " ELECTRON, ALPHA, OR SF  =  LIGHT-PARTICLE ENERGY + RECOIL ENERGY."

190 PRINT " NOTE: IN THE CASE OF ABSORPTION, THE TRANSITION ENERGY = "

200 PRINT " LIGHT-PARTICLE OR PHOTON ENERGY - RECOIL ENERGY."

210 PRINT " INSTEAD OF MASS M, IT IS A GOOD APPROX. TO USE THE MASS-NUMBER A."

220 PRINT

230 PRINT "*****************************************************************"

240 PRINT

250 PRINT " HIT A KEY WHEN READY"

260 IF INKEY$ = "" THEN 260 ELSE 270

270 COLOR 15, 1: CLS

280 LOCATE 3, 10: PRINT "THE ALTERNATIVES FOR RECOIL-ENERGY CALCULATION ARE:"

290 LOCATE 6, 10: PRINT "1: PHOTON DECAY (GAMMA, X-RAY, 'SOFT' PHOTON,..)"

300 LOCATE 8, 10: PRINT "2: BETA (CONTINUOUS SPECTRUM) DECAY"

310 LOCATE 10, 10: PRINT "3: EMISSION OF MONOKINETIC ELECTRON"

320 LOCATE 12, 10: PRINT "4: ALPHA DECAY "

330 LOCATE 14, 10: PRINT "5: SPONTANEOUS FISSION "

340 LOCATE 16, 10: PRINT "6: QUIT"

350 LOCATE 20, 10: PRINT "ENTER YOUR CHOICE BY NUMBER (1 TO 6): "

360 V$ = INKEY$: IF VAL(V$) < 1 OR VAL(V$) > 6 THEN 350

370 V = VAL(V$): COLOR 14, 5: CLS : PRINT

380 ON V GOTO 390, 480, 550, 640, 730, 800

390 INPUT " MASS OF RECOILING ATOM (IN a.m.u. = u) M = ", M1

400 PRINT

410 INPUT " PHOTON ENERGY (IN keV) E = ", E

420 ER = (5.538E-07) * E ^ 2 / M1

430 PRINT

440 PRINT USING " E(R) = ##.####^^^^ eV"; ER * 1000

450 PRINT

460 PRINT USING " TRANSITION ENERGY E(i) - E(f) = ##.####^^^^ keV"; E + ER

470 GOTO 220

480 INPUT " BETA-TRANSITION END-POINT ENERGY (MeV), E(max) = ", EM

490 PRINT

500 INPUT " MASS OF RECOILING ATOM (IN u) M = ", M2

510 PRINT

520 EMAX = EM * (EM + 1.022) / M2 / 1863: EMA = 1000000! * EMAX

530 PRINT USING " MAXIMUM RECOIL IN BETA-DECAY E(Rmax) = ##.##^^^^ eV"; EMA

540 GOTO 220

550 INPUT " ENERGY OF EMITTED ELECTRON (MeV), E = ", E1

560 PRINT

570 INPUT " MASS OF RECOILING ATOM (IN u) M = ", M3

580 PRINT

590 EEL = E1 * (E1 + 1.022) / M3 / 1863

600 PRINT USING " RECOIL ENERGY E(R) = ##.##^^^^ eV "; EEL * 1000000!

610 PRINT

620 PRINT " ENERGY IN CONV.-EL. TRANSITION = E + E(R) + EL. BINDING ENERGY."

630 GOTO 220

640 INPUT " ENERGY OF ALPHA-PARTICLE (MeV) E = ", E3

650 PRINT

660 INPUT " MASS OF MOTHER ATOM (IN u) M = ", M4

670 PRINT

680 EAR = E3 * 4.0026 / (M4 - 4.0026) + E3 ^ 2 / (1863 * (M4 - 4.0026))

690 PRINT USING " RECOIL ENERGY E(R) = ##.##^^^^ keV "; EAR * 1000

700 EQ = E3 + EAR: PRINT

710 PRINT USING " Q-VALUE OF THE TRANSITION = E + E(R) = ##.####^^^^ MeV"; EQ

720 GOTO 220

730 INPUT " MASSES OF FISSION FRAGMENTS (IN u) M(light), M(heavy) = ", M5, M6

740 PRINT

750 INPUT " SUM OF KINETIC ENERGIES OF FRAGMENTS (MeV) E = ", EF

760 PRINT

770 ERF = EF * M5 / (M5 + M6)

780 PRINT USING " (RECOIL) ENERGY OF HEAVY FRAGMENT E(h) = ##.## MeV "; ERF

790 GOTO 220

800 END

